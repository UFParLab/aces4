import "mcpt2_defs.sialx" 
import "mcpt2_vars.sialx"
import "tran_frag.sialx"
import "mcpt2_singles.sialx"
import "mcpt2_doubles.sialx"
import "mcpt2_util.sialx"
#
                           SIAL FRAG_2ORDER_CORR 
# 
# -----------------------------------------------------------------------------
# 
PROC FORM_JFRAG_HBAR
    server_barrier
    pardo fi,fi1
	put HBAR_ii[fi,fi1] = 0.0
    endpardo fi,fi1
    PARDO fa, fa1
	put HBAR_aa[fa,fa1] = 0.0
    ENDPARDO fa, fa1
    PARDO fa, fi
	put HBAR_ia[fi,fa]  = 0.0
    ENDPARDO fa, fi
    PARDO fi1,fa1,fa,fi
	    put HBAR_AJIB[fi1,fa1,fa,fi] = 0.0
    ENDPARDO fi1,fa1,fa,fi
    PARDO fi1,fi2,fa,fi
	put HBAR_IAJK[fi1,fi2,fa,fi] = 0.0
    ENDPARDO fi1,fi2,fa,fi
    PARDO fa1,fa2,fa,fi
	put HBAR_ABCI[fa1,fa2,fa,fi] = 0.0
    ENDPARDO fa1,fa2,fa,fi
    server_barrier

#-----------
    PARDO ifrag, jfrag, fi, fi1, b, j "Frag{NRij}{pp}{pp}"
    where ifrag != jfrag
    where (int)rcut_dist[ifrag,jfrag] == ifrag
    where (int)SwOccA_frag[(index)fi] == ifrag
    where (int)SwOccA_frag[(index)fi1] == ifrag
    where (int)SwVirtA_frag[(index)b] == jfrag
    where (int)SwOccA_frag[(index)j] == jfrag
	do fa1
	    where (int)SwVirtA_frag[(index)fa1] == ifrag

	    get Vaibj[fa1,fi1,b,j]
	    get T2old[fa1,fi,b,j]

	    tpp[fi,fi1] = T2old[fa1,fi,b,j]*Vaibj[fa1,fi1,b,j]
# 2 for alpha + beta jfrag
	    tpp[fi,fi1] *= 2.0

	    put HBAR_ii[fi,fi1] += tpp[fi,fi1]

	enddo fa1

    ENDPARDO ifrag, jfrag, fi, fi1, b, j
#-----------
    PARDO ifrag, jfrag, fa, fa1, b, j "Frag{NRij}{pp}{pp}"
    where ifrag != jfrag
    where (int)rcut_dist[ifrag,jfrag] == ifrag
    where (int)SwVirtA_frag[(index)fa] == ifrag
    where (int)SwVirtA_frag[(index)fa1] == ifrag
    where (int)SwVirtA_frag[(index)b] == jfrag
    where (int)SwOccA_frag[(index)j] == jfrag
	do fi
	where (int)SwOccA_frag[(index)fi] == ifrag

	    get Vaibj[fa1,fi,b,j]
	    get T2old[fa,fi,b,j]

	    tpp[fa,fa1] = T2old[fa,fi,b,j]*Vaibj[fa1,fi,b,j] 
# 2 for alpha + beta jfrag
	    tpp[fa,fa1]  *= -2.0

	    put HBAR_aa[fa,fa1] += tpp[fa,fa1]

	enddo fi

    ENDPARDO ifrag, jfrag, fa, fa1, b, j
#-----------
#-----------
    PARDO ifrag, jfrag, fa, fi, b, j "Frag{NRij}{pp}{pp}"
    where ifrag != jfrag
    where (int)rcut_dist[ifrag,jfrag] == ifrag
    where (int)SwVirtA_frag[(index)fa] == ifrag
    where (int)SwOccA_frag[(index)fi] == ifrag
    where (int)SwVirtA_frag[(index)b] == jfrag
    where (int)SwOccA_frag[(index)j] == jfrag

	get Vaibj[fa,fi,b,j]

	do fa1
	where (int)SwVirtA_frag[(index)fa1] == ifrag
	do fi1
	where (int)SwOccA_frag[(index)fi1] == ifrag

	    get T2old[fa1,fi1,b,j]

	    Tppqq[fi1,fa1,fa,fi] = T2old[fa1,fi1,b,j]*Vaibj[fa,fi,b,j]
# 2 for alpha + beta jfrag
	    Tppqq[fi1,fa1,fa,fi] *= 2.0

	    put HBAR_AJIB[fi1,fa1,fa,fi] += Tppqq[fi1,fa1,fa,fi]

	enddo fi1
	enddo fa1

    ENDPARDO ifrag, jfrag, fa, fi, b, j
#-----------
    PARDO ifrag, jfrag, fa, fi, b, j "Frag{NRij}{pp}{pp}"
    where ifrag != jfrag
    where (int)rcut_dist[ifrag,jfrag] == ifrag
    where (int)SwVirtA_frag[(index)fa] == ifrag
    where (int)SwOccA_frag[(index)fi] == ifrag
    where (int)SwVirtA_frag[(index)b] == jfrag
    where (int)SwOccA_frag[(index)j] == jfrag
	get T2old[fa,fi,b,j]

	do fi1
	where (int)SwOccA_frag[(index)fi1] == ifrag
	do fi2
	where (int)SwOccA_frag[(index)fi2] == ifrag

	get Viaii[j,b,fi1,fi2]
	T1ppqq[fi1,fi2,b,j] = Viaii[j,b,fi1,fi2]

	Tppqq[fi1,fi2,fa,fi]  = T1ppqq[fi1,fi2,b,j]*T2old[fa,fi,b,j]
# 2 for alpha + beta jfrag
	Tppqq[fi1,fi2,fa,fi] *= 2.0

	put HBAR_IAJK[fi1,fi2,fa,fi] += Tppqq[fi1,fi2,fa,fi]

	enddo fi2
	enddo fi1

    ENDPARDO ifrag, jfrag, fa, fi, b, j
#-----------
    PARDO ifrag, jfrag, fa, fi, b, j "Frag{NRij}{pp}{pp}"
    where ifrag != jfrag
    where (int)rcut_dist[ifrag,jfrag] == ifrag
    where (int)SwVirtA_frag[(index)fa] == ifrag
    where (int)SwOccA_frag[(index)fi] == ifrag
    where (int)SwVirtA_frag[(index)b] == jfrag
    where (int)SwOccA_frag[(index)j] == jfrag

	get T2old[fa,fi,b,j]

	do fa1
	where (int)SwVirtA_frag[(index)fa1] == ifrag
	do fa2
	where (int)SwVirtA_frag[(index)fa2] == ifrag

	get Vaaai[fa1,fa2,b,j]

	Tppqq[fa1,fa2,fa,fi] = Vaaai[fa1,fa2,b,j]*T2old[fa,fi,b,j]
# 2 for alpha + beta jfrag
	Tppqq[fa1,fa2,fa,fi] *= -2.0

	put HBAR_ABCI[fa1,fa2,fa,fi] += Tppqq[fa1,fa2,fa,fi]

	enddo fa2
	enddo fa1

    ENDPARDO ifrag, jfrag, fa, fi, b, j

###

    server_barrier
ENDPROC FORM_JFRAG_HBAR

PROC MOI_MANAGER
print "Forming HBAR elements"
call FORM_JFRAG_HBAR
print "done forming HBAR elements"

server_barrier
    pardo fa,fa1
	put FwC[fa,fa1] = 0.0
    endpardo fa,fa1
    pardo fa, fi
	put FwC[fa,fi] = 0.0
	put FwC[fi,fa] = 0.0
    endpardo fa, fi
    pardo fi,fi1
	put FwC[fi,fi1] = 0.0
    endpardo fi,fi1
    pardo fa, fi
	put FT1old[fa,fi] = 0.0
    endpardo fa, fi
    pardo fa, fi
	put FP2T1old[fa,fi] = 0.0
    endpardo fa, fi
    pardo fa, fi, fa1, fi1
	put FT2old[fa,fi,fa1,fi1] = 0.0
    endpardo fa, fi, fa1, fi1
server_barrier

    pardo fa, fi
        get p2t1old[fa,fi]
	put FP2T1old[fa,fi] = p2t1old[fa,fi]
    endpardo fa, fi

    pardo fa, fi, fa1, fi1
        get T2old[fa,fi,fa1,fi1]
	t1ppqq[fa,fi,fa1,fi1] = T2old[fa,fi,fa1,fi1]
	put FT2old[fa,fi,fa1,fi1] = t1ppqq[fa,fi,fa1,fi1]
    endpardo fa, fi, fa1, fi1

    esum1 = 0.0
    esum2 = 0.0
    esum4 = 0.0
    server_barrier

      PARDO fa, fi, fa1, fi1 

               GET                T2old[fa,fi,fa1,fi1]
               GET                Vaibj[fa,fi,fa1,fi1]
               GET                Vaibj[fa,fi1,fa1,fi]

               t1ppqq[fa,fi,fa1,fi1] = Vaibj[fa,fi,fa1,fi1]
               t3ppqq[fa,fi1,fa1,fi] = Vaibj[fa,fi1,fa1,fi]
               t2ppqq[fa,fi,fa1,fi1] = t3ppqq[fa,fi1,fa1,fi]
               t1ppqq[fa,fi,fa1,fi1]*= 2.0
               t1ppqq[fa,fi,fa1,fi1]-= t2ppqq[fa,fi,fa1,fi1]

               etemp = T2old[fa,fi,fa1,fi1]*t1ppqq[fa,fi,fa1,fi1]

               esum2 += etemp

      ENDPARDO fa, fi, fa1, fi1

    esum3 = 0.0
    do ifrag
    where ifrag == 1
	pardo jfrag
	WHERE jfrag > ifrag 
	where (int)rcut_dist[ifrag,jfrag] == ifrag

	get d20c[ifrag,jfrag]
	etemp = d20c[ifrag,jfrag]
	esum3 += etemp

	endpardo jfrag
    enddo ifrag

server_barrier
e1new = 0.0
e2new = 0.0
e3new = 0.0
e4new = 0.0
collective e2new += esum2
collective e3new += esum3
print e2new
print e3new

server_barrier
set_persistent FwC "MOI_wC"
set_persistent FT1old "MOI_T1"
set_persistent FP2T1old "MOI_p2T1"
set_persistent FT2old "MOI_T2ab"
set_persistent HBAR_ii "MOI_HBAR_ii"
set_persistent HBAR_aa "MOI_HBAR_aa"
set_persistent HBAR_ia "MOI_HBAR_ia"
set_persistent HBAR_AJIB "MOI_HBAR_ajib"
set_persistent HBAR_IAJK "MOI_HBAR_iajk"
set_persistent HBAR_ABCI "MOI_HBAR_abci"
server_barrier
ENDPROC MOI_MANAGER
#
# -----------------------------------------------------------------------------
# 
#     START OF MAIN PROGRAM  
#
# -----------------------------------------------------------------------------

print " "
print "-- Second-Order Molecular Cluster Perturbation Theory"
print "-- J. Comp. Chem. 2001, 22, 1574-1589"
print "-- Written by Victor Lotrich and Jason N. Byrd"
print " "

# 
#     Set fragment parameters   
#     ----------------------- 
#
      sip_barrier
      restore_persistent Dca "ca" 
      restore_persistent DFock_a "fock_a"
      rmaster = 0
      do_nonhf = 0
      zero = 0.0 
      one  = 1.0 
      print r_thresh 
      print elst_thresh 
      mone = -1.0 
      print cc_conv 
      sip_barrier
#
      CALL SET_FRAG_PARMS   
      sip_barrier 
      print "Done setting fragment parameters" 
      call set_fock_mos
      sip_barrier 
      CALL FRAG_TRAN_NO_WC
      sip_barrier 
      print "Done two-electron integral transformation" 
      sip_barrier 
      call FORM_ELSTPOT
      sip_barrier 
      call OVERLAP_EXCHANGE
      sip_barrier 
      pardo ifrag, a,i "Frag{i}{pp}{}"
          put wC[a,i] = 0.0
      endpardo ifrag, a,i
      sip_barrier 
      call DOUBLES_MANAGER
      sip_barrier
#
      call END_PRINT
      sip_barrier

    print "starting additional transformations"
    call ADDITIONAL_FRAG_TRAN
    print "Finished additional transformations"

    call form_t1_secondorder

    sip_barrier

    call MOI_MANAGER

         sip_barrier 

                           ENDSIAL FRAG_2ORDER_CORR 
##
###############################################################################
