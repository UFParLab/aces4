import "mcpt2_defs.sialx" 
import "mcpt2_vars.sialx"
import "tran_frag.sialx"
import "mcpt2_singles.sialx"
import "mcpt2_doubles.sialx"
import "mcpt2_util.sialx"
#
                           SIAL FRAG_2ORDER_CORR 
# 
# -----------------------------------------------------------------------------
# 
PROC FORM_JFRAG_HBAR
    server_barrier

#do ifrag
#where ifrag == 1
#-----------
    PARDO ifrag, jfrag, fi, fi1, b, j "Frag{NRij}{oo}{vo}"
    where ifrag != jfrag
    where (int)rcut_dist[ifrag,jfrag] == ifrag
    where (int)SwOccA_frag[(index)fi] == ifrag
    where (int)SwOccA_frag[(index)fi1] == ifrag
    where (int)SwVirtA_frag[(index)b] == jfrag
    where (int)SwOccA_frag[(index)j] == jfrag
	get Viaii[j,b,fi,fi1]
	get t1old[b,j]

	tpp[fi,fi1]  = t1old[b,j]*Viaii[j,b,fi,fi1]
# 2 for alpha + beta jfrag
	tpp[fi,fi1] *= 2.0

	put HBAR_ii[fi,fi1] += tpp[fi,fi1]

    ENDPARDO ifrag, jfrag, fi, fi1, b, j

    PARDO ifrag, jfrag, fi, fi1, b, j "Frag{NRij}{oo}{vo}"
    where ifrag != jfrag
    where (int)rcut_dist[ifrag,jfrag] == ifrag
    where (int)SwOccA_frag[(index)fi] == ifrag
    where (int)SwOccA_frag[(index)fi1] == ifrag
    where (int)SwVirtA_frag[(index)b] == jfrag
    where (int)SwOccA_frag[(index)j] == jfrag
	do fa1
	    where (int)SwVirtA_frag[(index)fa1] == ifrag

	    get Vaibj[fa1,fi1,b,j]
	    get T2old[fa1,fi,b,j]

	    tpp[fi,fi1] = T2old[fa1,fi,b,j]*Vaibj[fa1,fi1,b,j]
# 2 for alpha + beta jfrag
	    tpp[fi,fi1] *= 2.0

	    put HBAR_ii[fi,fi1] += tpp[fi,fi1]

	enddo fa1

    ENDPARDO ifrag, jfrag, fi, fi1, b, j
#-----------
    PARDO ifrag, jfrag, fa, fa1, b, j "Frag{NRij}{vv}{vo}"
    where ifrag != jfrag
    where (int)rcut_dist[ifrag,jfrag] == ifrag
    where (int)SwVirtA_frag[(index)fa] == ifrag
    where (int)SwVirtA_frag[(index)fa1] == ifrag
    where (int)SwVirtA_frag[(index)b] == jfrag
    where (int)SwOccA_frag[(index)j] == jfrag
	get Vaaai[fa,fa1,b,j]
	get t1old[b,j]

	tpp[fa,fa1]  = Vaaai[fa,fa1,b,j]*t1old[b,j]
# 2 for alpha + beta jfrag
	tpp[fa,fa1] *= 2.0

	put HBAR_aa[fa,fa1] += tpp[fa,fa1]

    ENDPARDO ifrag, jfrag, fa, fa1, b, j

    PARDO ifrag, jfrag, fa, fa1, b, j "Frag{NRij}{vv}{vo}"
    where ifrag != jfrag
    where (int)rcut_dist[ifrag,jfrag] == ifrag
    where (int)SwVirtA_frag[(index)fa] == ifrag
    where (int)SwVirtA_frag[(index)fa1] == ifrag
    where (int)SwVirtA_frag[(index)b] == jfrag
    where (int)SwOccA_frag[(index)j] == jfrag
	do fi
	where (int)SwOccA_frag[(index)fi] == ifrag

	    get Vaibj[fa1,fi,b,j]
	    get T2old[fa,fi,b,j]

	    tpp[fa,fa1] = T2old[fa,fi,b,j]*Vaibj[fa1,fi,b,j] 
# 2 for alpha + beta jfrag
	    tpp[fa,fa1]  *= -2.0

	    put HBAR_aa[fa,fa1] += tpp[fa,fa1]

	enddo fi

    ENDPARDO ifrag, jfrag, fa, fa1, b, j
#-----------
    PARDO ifrag, jfrag, fa, fi, b, j "Frag{NRij}{vo}{vo}"
    where ifrag != jfrag
    where (int)rcut_dist[ifrag,jfrag] == ifrag
    where (int)SwVirtA_frag[(index)fa] == ifrag
    where (int)SwOccA_frag[(index)fi] == ifrag
    where (int)SwVirtA_frag[(index)b] == jfrag
    where (int)SwOccA_frag[(index)j] == jfrag
	get Vaibj[fa,fi,b,j]
	get t1old[b,j]

	tpp[fi,fa]  = Vaibj[fa,fi,b,j]*t1old[b,j]
# 2 for alpha + beta jfrag
	tpp[fi,fa] *= 2.0

	put HBAR_ia[fi,fa] += tpp[fi,fa]

    ENDPARDO ifrag, jfrag, fa, fi, b, j
#-----------
    PARDO ifrag, jfrag, fa, fi, b, j "Frag{NRij}{vo}{vo}"
    where ifrag != jfrag
    where (int)rcut_dist[ifrag,jfrag] == ifrag
    where (int)SwVirtA_frag[(index)fa] == ifrag
    where (int)SwOccA_frag[(index)fi] == ifrag
    where (int)SwVirtA_frag[(index)b] == jfrag
    where (int)SwOccA_frag[(index)j] == jfrag

	get Vaibj[fa,fi,b,j]

	do fa1
	where (int)SwVirtA_frag[(index)fa1] == ifrag
	do fi1
	where (int)SwOccA_frag[(index)fi1] == ifrag

	    get T2old[fa1,fi1,b,j]

	    Tppqq[fi1,fa1,fa,fi] = T2old[fa1,fi1,b,j]*Vaibj[fa,fi,b,j]
# 2 for alpha + beta jfrag
	    Tppqq[fi1,fa1,fa,fi] *= 2.0

	    put HBAR_AJIB[fi1,fa1,fa,fi] += Tppqq[fi1,fa1,fa,fi]

	enddo fi1
	enddo fa1

    ENDPARDO ifrag, jfrag, fa, fi, b, j
#-----------
    PARDO ifrag, jfrag, fa, fi, b, j "Frag{NRij}{vo}{vo}"
    where ifrag != jfrag
    where (int)rcut_dist[ifrag,jfrag] == ifrag
    where (int)SwVirtA_frag[(index)fa] == ifrag
    where (int)SwOccA_frag[(index)fi] == ifrag
    where (int)SwVirtA_frag[(index)b] == jfrag
    where (int)SwOccA_frag[(index)j] == jfrag
	get T2old[fa,fi,b,j]

	do fi1
	where (int)SwOccA_frag[(index)fi1] == ifrag
	do fi2
	where (int)SwOccA_frag[(index)fi2] == ifrag

	get Viaii[j,b,fi1,fi2]
	T1ppqq[fi1,fi2,b,j] = Viaii[j,b,fi1,fi2]

	Tppqq[fi1,fi2,fa,fi]  = T1ppqq[fi1,fi2,b,j]*T2old[fa,fi,b,j]
# 2 for alpha + beta jfrag
	Tppqq[fi1,fi2,fa,fi] *= 2.0

	put HBAR_IAJK[fi1,fi2,fa,fi] += Tppqq[fi1,fi2,fa,fi]

	enddo fi2
	enddo fi1

    ENDPARDO ifrag, jfrag, fa, fi, b, j
#-----------
    PARDO ifrag, jfrag, fa, fi, b, j "Frag{NRij}{vo}{vo}"
    where ifrag != jfrag
    where (int)rcut_dist[ifrag,jfrag] == ifrag
    where (int)SwVirtA_frag[(index)fa] == ifrag
    where (int)SwOccA_frag[(index)fi] == ifrag
    where (int)SwVirtA_frag[(index)b] == jfrag
    where (int)SwOccA_frag[(index)j] == jfrag

	get T2old[fa,fi,b,j]

	do fa1
	where (int)SwVirtA_frag[(index)fa1] == ifrag
	do fa2
	where (int)SwVirtA_frag[(index)fa2] == ifrag

	get Vaaai[fa1,fa2,b,j]

	Tppqq[fa1,fa2,fa,fi] = Vaaai[fa1,fa2,b,j]*T2old[fa,fi,b,j]
# 2 for alpha + beta jfrag
	Tppqq[fa1,fa2,fa,fi] *= -2.0

	put HBAR_ABCI[fa1,fa2,fa,fi] += Tppqq[fa1,fa2,fa,fi]

	enddo fa2
	enddo fa1

    ENDPARDO ifrag, jfrag, fa, fi, b, j

###
#enddo ifrag

    server_barrier
ENDPROC FORM_JFRAG_HBAR
#
# -----------------------------------------------------------------------------
# 
#     START OF MAIN PROGRAM  
#
# -----------------------------------------------------------------------------

print " "
print "-- Second-Order Molecular Cluster Perturbation Theory"
print "-- J. Comp. Chem. 2001, 22, 1574-1589"
print "-- Written by Victor Lotrich and Jason N. Byrd"
print " "

# 
#     Set fragment parameters   
#     ----------------------- 
#
      sip_barrier
      restore_persistent Dca "ca" 
      restore_persistent DFock_a "fock_a"
      rmaster = 0
      allocate sp[*] 
      zero = 0.0 
      one  = 1.0 
      print r_thresh 
      print elst_thresh 
      mone = -1.0 
      print cc_conv 
      sip_barrier
#
      CALL SET_FRAG_PARMS   
      sip_barrier 
      print "Done setting fragment parameters" 
      call set_fock_mos
      sip_barrier 


#     Compute the NN repusion between fragments  
#     ----------------------------------------- 
      nn_rep = 0.0 
      execute compute_nn_repulsion nn_rep
      print nn_rep 
#
#     Perform 2-el integral transformation and calculate second order energies   
#     ------------------------------------------------------------------------ 
#
      sip_barrier 
      CALL FRAG_TRAN 
      sip_barrier 
      print "Done two-electron integral transformation" 
#
#     Form the electrostatic potential(complete) of all fragments  
#     ----------------------------------------------------------- 
#
      CALL REFORM_WC
      CALL FORM_ELSTPOT 
      sip_barrier 
      print "Done forming electrostatic potential" 
# Now we will compute the Hartree-Fock exchange energy 
# Compute the exchange and polarization energies 
      CALL OVERLAP_TRANS 
      print "Done transforming overlap" 
#
#     Form the density based exchange energy  
#     -------------------------------------- 
#
      CALL EXCHANGE_ALT
      print "Done forming density based exchange energy" 
#
#     Compute the CCPT(2) dimer_doubles amplitudes for Fragment 1.  
#     ------------------------------------------------------ 
#
      print " "
      CALL FORM_T10
      sip_barrier 
      print " "
      CALL FORM_T1
      sip_barrier 
      print "Completely Done singles formation" 
      print e1x_at 
      print e10pol_at 
      print eps1_pol  
      print singles  
      sip_barrier 
#
#     Compute the CCPT(2) dimer_doubles amplitudes for Fragment 1.  
#     ------------------------------------------------------ 
      CALL FORM_T2_0GUESS 
      sip_barrier 
      print "Done forming T2_0guess" 
#
# Start macro-iterations 
      DO jiter 
         sip_barrier 
	 print " "
	 print "------------------------------------------------------"
	 print "Macro iteration"
	 print jiter
	 print " "

         PARDO ifrag 
           GET                   e20c[ifrag] 
           PUT e2_macro[ifrag] = e20c[ifrag] 
         ENDPARDO ifrag  

         sip_barrier 
         CALL FORM_T2_GUESS 
         sip_barrier 
	 print "Entering FORM_T2_FRAGMENT_ITERS"
         CALL FORM_T2_FRAGMENT_ITERS 
         sip_barrier 
	 print " "
	 print "Entering FORM_T2_DIMER_ITERS"
         CALL FORM_T2_DIMER_ITERS 
         print "Done forming T2 Dimer calculation" 
         esum = 0.0 
         sip_barrier 
# Print out energies after each macroiteration  
         execute get_my_rank rank
         if rank == 0.0
	 print " "
         print "MCPT(2) monomer macro-iteration energies"
	 fragment_doubles = 0.0
         DO ifrag
            allocate contiguous macro_e20[ifrag:ifrag]
            GET                 e20c[ifrag] 
            macro_e20[ifrag:ifrag]  = e20c[ifrag] 
            print macro_e20[ifrag:ifrag] 
            etemp = e20c[ifrag] 
            fragment_doubles += etemp 
            deallocate contiguous macro_e20[ifrag:ifrag]
         ENDDO ifrag   

	 print " "
         print "MCPT(2) dimer macro-iteration energies"
	 do ifrag
	 do jfrag
	     WHERE jfrag > ifrag 
	     where (int)rcut_dist[ifrag,jfrag] == ifrag
	     icount = (scalar)ifrag
	     jcount = (scalar)jfrag

		 allocate contiguous macro_d20[ifrag:ifrag,jfrag:jfrag]
		 get d20c[ifrag,jfrag]
		 macro_d20[ifrag:ifrag,jfrag:jfrag] = d20c[ifrag,jfrag]
		 print macro_d20[ifrag:ifrag,jfrag:jfrag]
		 deallocate contiguous macro_d20[ifrag:ifrag,jfrag:jfrag]
	 enddo jfrag
	 enddo ifrag
         endif
         sip_barrier 

# Determine if full macro convergence is met 
         do_iter = 0.0 
         DO ifrag 
           GET          e20c[ifrag] 
           GET          e2_macro[ifrag] 
           tp[ifrag]  = e20c[ifrag]  
           tp[ifrag] -= e2_macro[ifrag]  
           execute return_sval tp[ifrag] etemp  
           if etemp < zero 
              etemp *= -1.0 
           endif 
           IF etemp > cc_conv 
              do_iter = 2.0 
           ENDIF 
         ENDDO ifrag 
         print dimer_doubles  
         print fragment_doubles  
         print etemp 
         sip_barrier 
         if do_iter < 1.0 
            exit 
         endif 
#
      ENDDO jiter 
      sip_barrier
#
      CALL FORM_T2_MONOMER  
      sip_barrier
#
      eint_tot  = 0.0 
      eint_tot += e1x_at  
      eint_tot += e10pol_at  
      eint_tot += eps1_pol   
      eint_tot += singles  
      eint_tot += dimer_doubles  
      eint_tot += fragment_doubles  
      eint_tot += mono_lccd   

      print e1x_at 
      print e10pol_at 
      print eps1_pol  
      print singles  
      print dimer_doubles  
      print fragment_doubles  
      print mono_lccd  

      print eint_tot 

#
print "starting additional transformations"
call ADDITIONAL_FRAG_TRAN
print "Finished additional transformations"
print "Forming HBAR elements"
call FORM_JFRAG_HBAR
print "done forming HBAR elements"

server_barrier
do ifrag
where ifrag == 1

    pardo fa,fa1
        get wC[fa,fa1]
	put FwC[fa,fa1] = wC[fa,fa1]
    endpardo fa,fa1
    pardo fa, fi
	get wC[fa,fi]
	t1ai[fa,fi] = wC[fa,fi]
	t1ia[fi,fa] = wC[fa,fi]
	put FwC[fa,fi] = t1ai[fa,fi]
	put FwC[fi,fa] = t1ia[fi,fa]
    endpardo fa, fi
    pardo fi,fi1
        get wC[fi,fi1]
	put FwC[fi,fi1] = wC[fi,fi1]
    endpardo fi,fi1

    pardo fa, fi
        get t1old[fa,fi]
	put FT1old[fa,fi] = t1old[fa,fi]
    endpardo fa, fi

    pardo fa, fi, fa1, fi1

        get T2old[fa,fi,fa1,fi1]

	t1ppqq[fa,fi,fa1,fi1] = T2old[fa,fi,fa1,fi1]

	put FT2old[fa,fi,fa1,fi1] = t1ppqq[fa,fi,fa1,fi1]
	
    endpardo fa, fi, fa1, fi1

esum1 = 0.0
esum2 = 0.0
    server_barrier

      PARDO fa, fi, fa1, fi1 

               GET                T2old[fa,fi,fa1,fi1]
               GET                Vaibj[fa,fi,fa1,fi1]
               GET                Vaibj[fa,fi1,fa1,fi]

               t1ppqq[fa,fi,fa1,fi1] = Vaibj[fa,fi,fa1,fi1]
               t3ppqq[fa,fi1,fa1,fi] = Vaibj[fa,fi1,fa1,fi]
               t2ppqq[fa,fi,fa1,fi1] = t3ppqq[fa,fi1,fa1,fi]
               t1ppqq[fa,fi,fa1,fi1]*= 2.0
               t1ppqq[fa,fi,fa1,fi1]-= t2ppqq[fa,fi,fa1,fi1]

               etemp = T2old[fa,fi,fa1,fi1]*t1ppqq[fa,fi,fa1,fi1]

               esum2 += etemp

      ENDPARDO fa, fi, fa1, fi1

      PARDO fa, fi

               GET        FT1old[fa,fi] 
               GET        FwC[fa,fi] 
               tai[fa,fi] = FT1old[fa,fi] 

               etemp = tai[fa,fi]*FwC[fa,fi]  
               etemp*= 2.0  

	       esum1 += etemp

      ENDPARDO fa, fi

server_barrier
e1new = 0.0
e2new = 0.0
collective e1new += esum1
collective e2new += esum2
print ifrag
print e1new
print e2new

enddo ifrag

server_barrier
set_persistent FwC "MOI_wC"
set_persistent FT1old "MOI_T1"
set_persistent FT2old "MOI_T2ab"
set_persistent HBAR_ii "MOI_HBAR_ii"
set_persistent HBAR_aa "MOI_HBAR_aa"
set_persistent HBAR_ia "MOI_HBAR_ia"
set_persistent HBAR_AJIB "MOI_HBAR_ajib"
set_persistent HBAR_IAJK "MOI_HBAR_iajk"
set_persistent HBAR_ABCI "MOI_HBAR_abci"

         sip_barrier 
                           ENDSIAL FRAG_2ORDER_CORR 
##
################################################################################
