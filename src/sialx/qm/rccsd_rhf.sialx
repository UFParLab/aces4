#  Copyright (c) 2003-2010 University of Florida
  import "rccsd_rhf_defs.sialx" 
#
#  This program is free software; you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation; either version 2 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  The GNU General Public License is included in this distribution
#  in the file COPYRIGHT.
                           SIAL CCSD_RHF_SV1
#
#-------------------------------------------------------------------------------
#
# Declare indeces 
# --------------- 
#
      index kiter    = 1: cc_iter   
      index   kbeg   = 1: scf_beg # Change to cc_beg 
      index   korder = 1: scf_hist # Change to cc_hist 
      index   kone   = 1: 1
      index   kdiis  = 1: 30 
      index   k1diis = 1: 30 
      index   jdiis  = 1: 30 
      index   j1diis = 1: 30 
      index      D1  = 1: 30 
      index      D2  = 1: 30 
#
      aoindex mu     = 1: norb 
      aoindex nu     = 1: norb
      aoindex lambda = 1: norb
      aoindex sigma  = 1: norb
#
      moaindex i = baocc: eaocc
      moaindex i1= baocc: eaocc
      moaindex i2= baocc: eaocc
      moaindex i3= baocc: eaocc
#
      moaindex a = bavirt: eavirt
      moaindex a1= bavirt: eavirt
      moaindex a2= bavirt: eavirt
      moaindex a3= bavirt: eavirt
#
      moaindex j = baocc: eaocc
      moaindex j1= baocc: eaocc
      moaindex j2= baocc: eaocc
      moaindex j3= baocc: eaocc
#
      moaindex b = bavirt: eavirt
      moaindex b1= bavirt: eavirt
      moaindex b2= bavirt: eavirt
      moaindex b3= bavirt: eavirt
#
      moaindex p = baocc: eavirt
      moaindex p1= baocc: eavirt
      moaindex p2= baocc: eavirt
      moaindex p3= baocc: eavirt
#
      moaindex q = baocc: eavirt
      moaindex q1= baocc: eavirt
      moaindex q2= baocc: eavirt
      moaindex q3= baocc: eavirt
# 
# Declare static arrays 
# --------------------- 
#
      distributed t1a_old[a,i] 
      distributed t1a_new[a,i] 
#
      distributed Fae_a[a,a1] 
      distributed Fme_a[i,a] 
      distributed Fmi_a[i,i1] 
      distributed DCa[mu,p] 
      distributed FTa[p,p1] 

      static zz[mu,nu] 
      static ca[mu,p] 
      static fock_a[p,p1] 
#
# Arrays used in the DIIS 
# -----------------------
#
      served Daibj[a,i,b,j,kdiis] 
      served Eaibj[a,i,b,j,jdiis] 
      distributed Dai[a,i,kdiis] 
      distributed Eai[a,i,jdiis] 
      served D0aibj[a,i,b,j] 
      served D0ai[a,i] 

      static BB[d1,d2] 
      temp tbb[d1,d2] 
      distributed DIST_BB[d1,d2] 
      scalar n1 
      scalar n2 
      scalar worder  
#
# Arrays used in transformation for AO2 algorithm
# -----------------------------------------------
#
      temp Txixi[mu,i1,lambda,i]
      temp Txxii[mu,nu,i1,i]
      temp Tixxi[i1,nu,lambda,i]
      temp Txipi[mu,i,p,i1]
      temp Tpipi[p1,i,p,i1]
      temp T1pipi[p1,i,p,i1]
      temp Tixai[i,mu,a,i1]
      temp Txaii[mu,a,i,i1]
      temp Tiaai[i,a1,a,i1]
      temp Taaii[a,a1,i,i1]
      temp Txaai[mu,a1,a,i]
      temp Taaai[a2,a,a1,i]
      temp Txxai[mu,nu,a,i]
#
      served VSpipi[p1,i,p,i1]
      served Viaai[i,a1,a,i1] 
      served Vaaii[a,a1,i,i1]
      served Vaaai[a2,a,a1,i]
#
      temp Txixj[mu,i,nu,j]
      served Vpiqj[p,i,q,j]
#
# End Arrays used in transformation for AO2 algorithm
# --------------------------------------------------- 
#
# Declare temporary arrays 
# ------------------------ 
#
      temp Txxxi[mu,nu,lambda,i]
      temp T1xixi[mu,i,nu,i1]
      temp Txiix[mu,i,i1,nu]
      temp Txjjx[mu,j,j1,nu]
      temp Txijx[mu,i,j,nu]
      temp Txpii[mu,p,i,i1]
      temp Txqii[mu,q,i,i1]
      temp Tppii[p,p1,i,i1]
      temp Tqqii[q,q1,i,i1]
      temp Txpjj[mu,p,j,j1]
      temp Txqjj[mu,q,j,j1]
      temp Tppjj[p,p1,j,j1]
      temp Tqqjj[q,q1,j,j1]
      temp Tixpi[i,mu,p,i1]
      temp Tippi[i,p,p1,i1]
      temp Tjxqj[j,mu,q,j1]
      temp Tjqqj[j,q,q1,j1]
      temp Tixqj[i,mu,q,j]
      temp Tipqj[i,p,q,j]
#
      temp Taiai[p,i,p1,i1]
      temp T1aiai[p,i,p1,i1]
      temp T2aiai[p,i,p1,i1]
      temp T3aiai[p,i,p1,i1]
      temp Tbjbj[q,j,q1,j1]
      temp T1bjbj[q,j,q1,j1]
      temp T2bjbj[q,j,q1,j1]
      temp T3bjbj[q,j,q1,j1]
      temp Taibj[p,i,q,j]
      temp T1aibj[p,i,q,j]
      temp T2aibj[p,i,q,j]
      temp T3aibj[p,i,q,j]
      temp T4aibj[p,i,q,j]
      temp T5aibj[p,i,q,j]
#
      temp Txxxp[mu,nu,lambda,i]
      temp Txxxq[mu,nu,lambda,j]
      temp Txxpp[mu,nu,p1,i]
      temp Txxqq[mu,nu,q1,j]
      temp Txppp[mu,p2,p1,i]
      temp Txqpp[mu,q,p1,i]
      temp Txqqq[mu,q2,q1,j]
      temp Txpqq[mu,p,q1,j]
      temp Tpppp[p3,p2,p1,p]
      temp Tqqqq[q3,q2,q1,q]
      temp Tppqq[p1,p,q1,j]
      temp Tqqpp[q1,q,p1,i]
      temp Tixxx[i,mu,nu,sigma] 
      temp Tipxx[i,p,nu,sigma] 
      temp Tipqx[i,p,q,sigma] 
      temp Tipqq[i,p,q,q1] 
      temp Txiai[lambda,i,a1,i1] 
      temp Txjbj[lambda,j,b1,j1] 
      temp Txibj[lambda,i,b1,j1] 
#
      temp tmp1_aiai[a,i,a1,i1] 
      temp tmp2_aiai[a,i,a1,i1] 
      temp tmp3_aiai[a,i,a1,i1] 
      temp tmp1_aibj[a,i,b,j] 
      temp tmp2_aibj[a,i,b,j] 
#
      temp Tai[a,i] 
      temp T1ai[a,i] 
      temp T2ai[a,i] 
      temp T3ai[a,i] 
      temp Tbj[b,j] 
      temp T1bj[b,j] 
      temp T2bj[b,j] 
#
      temp Taa[a,a1] 
      temp T1aa[a,a1] 
      temp T1bb[b,b1] 
      temp Tae_a[a,a1] 
      temp Tae_b[b,b1] 
#
      temp Tia[i,a] 
      temp T1ia[i,a] 
      temp Tjb[j,b] 
      temp T1jb[j,b] 
      temp Tme_a[i,a] 
      temp Tme_b[j,b] 
#
      temp Tii[i,i1] 
      temp T1ii[i,i1] 
      temp Tjj[j,j1] 
      temp T1jj[j,j1] 
      temp Tmi_a[i,i1] 
      temp Tmi_b[j,j1] 
#
      temp T1pppp[p,p1,p2,p3] 
      temp T1qqqq[q,q1,q2,q3] 
      temp Taiii[a,i,i1,i2] 
      temp T1aiii[a,i,i1,i2] 
#
      temp  Tiiii[i,i1,i2,i3] 
      temp T1iiii[i,i1,i2,i3] 
#
      temp  Tjjjj[j,j1,j2,j3] 
      temp T1jjjj[j,j1,j2,j3] 
      temp  Tbjjj[b,j,j1,j2] 
      temp T1bjjj[b,j,j1,j2] 
#
      temp  Tiijj[i,i1,j2,j3] 
      temp T1iijj[i,i1,j2,j3] 
#
      temp  tmp_aa[a,a1] 
      temp tmp1_aa[a,a1] 
      temp  tmp_ii[i,i1] 
      temp tmp1_ii[i,i1] 
#
      temp  Tiiai[i,i1,a,i2] 
      temp T1iiai[i,i1,a,i2] 
#
      temp  tmp_bb[b,b1] 
      temp tmp1_bb[b,b1] 
      temp  tmp_jj[j,j1] 
      temp tmp1_jj[j,j1] 
#
      temp  Tjjbj[j,j1,b,j2] 
      temp T1jjbj[j,j1,b,j2] 
# 
      temp T1aaai[a,a1,a2,i]  
      temp T2aaai[a,a1,a2,i]  
      temp Taaaa[a,a1,a2,a3]  
      temp T1aaaa[a,a1,a2,a3]  
#
      temp T1bbbj[b,b1,b2,j]  
      temp T2bbbj[b,b1,b2,j]  
      temp Tbbbb[b,b1,b2,b3]  
      temp T1bbbb[b,b1,b2,b3]  
#
      temp Taabb[a,a1,b,b1]  
      temp T1aabb[a,a1,b,b1]  
#
      temp R1aiai[a,i,a1,i1] 
      temp R2aiai[a,i,a1,i1] 
      temp R3aiai[a,i,a1,i1] 
      temp R4aiai[a,i,a1,i1] 
      temp R1bjbj[b,j,b1,j1] 
      temp R2bjbj[b,j,b1,j1] 
      temp R3bjbj[b,j,b1,j1] 
      temp R4bjbj[b,j,b1,j1] 
      temp R1aibj[a,i,b,j] 
      temp R2aibj[a,i,b,j] 
      temp R3aibj[a,i,b,j] 
      temp R4aibj[a,i,b,j] 
#
      temp T1iaai[i,a,a1,i1] 
      temp T2iaai[i,a,a1,i1] 
      temp T1jbbj[j,b,b1,j1] 
      temp T2jbbj[j,b,b1,j1] 
      temp T1iabj[i,a,b,j] 
      temp T2iabj[i,a,b,j] 
      temp Tjjpp[j,j1,p,p1] 
      temp T1jjpp[j,j1,p,p1] 
      temp T2jjpp[j,j1,p,p1] 
      temp  Tiiqq[i,i1,q,q1] 
      temp T1iiqq[i,i1,q,q1] 
      temp T2iiqq[i,i1,q,q1] 
      temp  Tjjai[j,j1,a,i] 
      temp T1jjai[j,j1,a,i] 
      temp T2jjai[j,j1,a,i] 
      temp  Tiibj[i,i1,b,j] 
      temp T1iibj[i,i1,b,j] 
      temp T2iibj[i,i1,b,j] 
      temp  Tjbai[j,b,a,i]  
      temp T1jbai[j,b,a,i]  
      temp T2jbai[j,b,a,i]  
      temp  Taijj[a,i,j,j1] 
      temp T1aijj[a,i,j,j1] 
      temp Tqqip[a,i,b,b1]  
#
      temp tpx[p,mu] 
      temp t1px[p,mu] 
      temp tqx[q,mu] 
      temp t1qx[q,mu] 
      temp txi[mu,i] 
      temp t1xi[mu,i] 
      temp txj[mu,j] 
      temp t1xj[mu,j] 
#
# Declare distributed arrays 
# -------------------------- 
#
      served T2new_ab[a,i,b,j]
      served T2old_aa[a,i,a1,i1]
      served T2old_ab[a,i,b,j]
#
      served Tau_ab[a,i,b,j]
      served Taup_aa[a,i,a1,i1]
      served Taup_ab[a,i,b,j]
#
      served Wiibb[i1,i,b,b1]    
      served Wjjaa[j1,j,a,a1]      
#
# Declare served arrays 
# --------------------- 
#
      temp AOINT[mu,nu,lambda,sigma] 
      local LTAO_ab[lambda,i,sigma,i1] 
      scalar ncount1
      scalar ncount2
      scalar jcount 
      scalar kcount 
      scalar niter 
      scalar diis_start 
      scalar diis_order  
      scalar temp_order  
      scalar zero
      scalar one
      scalar two
      scalar five
      scalar six
      scalar seven
      scalar energy 
#
# Arrays needed for AOLADDER contribution
# ---------------------------------------
#
     temp Zab[lambda,i,b,j]
     temp Yab[lambda,i,mu,j]
     temp Y1ab[lambda,i,mu,j]
#
     temp TXaiai[a,i,nu,i1]
     temp Taixi[a,i,nu,i1]   
     temp TYaiai[a,i,a1,i1]
     temp TY1aiai[a,i,a1,i1]
     temp TXbjbj[b,j,nu,j1]
     temp Tbjxj[b,j,nu,j1]
     temp TYbjbj[b,j,b1,j1]
     temp TY1bjbj[b,j,b1,j1]
     temp TXaibj[a,i,nu,j]
     temp TZaibj[a,i,nu,j]
     temp Taixj[a,i,nu,j]
     temp TYaibj[a,i,b,j]
#
     served TAO_ab[lambda,i,sigma,j]
     served T2AO_ab[lambda,i,sigma,j]
     distributed t1a_ax[a,mu] 
     distributed t1a_xi[mu,i] 
     distributed t1b_xj[mu,j] 
     served TDaixj[a,i,nu,j]
     local LDaixj[a,i,nu,j] 
#
# local arrays used for data storage
# ---------------------------------- 
#
     local Liiai[i2,i,a,i1] 
     local L1iiai[i,i1,a2,i2] 
     local L2iiai[i2,i1,a2,i]  
     local L3iiai[i,i1,a,i2]  
     local L4iiai[i2,i1,a,i]  
     local Laiai[a,i,a2,i1] 
     local L1aiai[a,i,a1,i2] 
     local L2aiai[a1,i1,a2,i] 
     local L3aiai[a2,i1,a,i2] 
     local L4aiai[a1,i,a2,i1] 
     local Laaai[a,a2,a1,i] 
#
     local Ljjbj[j2,j,b,j1] 
     local Lbjbj[b,j,b2,j1] 
     local L1bjbj[b,j,b1,j2] 
     local L2bjbj[b1,j1,b2,j] 
     local L3bjbj[b2,j1,b,j2] 
     local L4bjbj[b1,j,b2,j1] 
     local Lbbbj[b,b2,b1,j] 
#
     local Laijj[a,i,j1,j] 
     local Laibj[a,i,b1,j] 
     local L1aibj[a1,i,b,j] 
     local L2aibj[a,i,b,j2] 
     local L3aibj[a,i1,b,j] 
     local L4aibj[a,i,b1,j] 
     local Liibj[i1,i,b,j] 
     local L1iibj[i,i1,b,j] 
     local Lbbai[b,b1,a,i] 
     local Laabj[a,a1,b,j] 
     local LLaiai[a,i,a1,i1] 
     local Lxiai[lambda,i,a1,i1] 
     local LLbjbj[b,j,b1,j1] 
     local Lxjbj[lambda,j,b1,j1] 
     local LLaibj[a,i,b,j] 
     local Lxibj[lambda,i,b,j] 
     local L1xixi[mu,i,nu,i1]  
     local L2xixi[nu,i1,mu,i]  
     local L1xjxj[mu,j,nu,j1]  
     local L2xjxj[nu,j1,mu,j]  
     local L1xixj[mu,i,nu,j]  
     local Laiii[a,i,i2,i1] 
     local L1aiii[a1,i2,i,i1] 
     local Lbjjj[b,j,j2,j1] 
     local L1bjjj[b1,j2,j,j1] 
     local Ljjai[a,i,j,j1] 
#
     local LWaaai[a2,a,a1,i1] 
     local LWbbbj[b2,b,b1,j1] 
     local LWbbai[b1,b,a,i] 
     local LWaabj[a1,a,b,j] 
     local LXbbai[b1,b,a1,i1] 
     local LXaabj[a1,a,b1,j1] 
     local LWaiai[a2,i,a,i1] 
     local LWbjbj[b2,j,b,j1] 
     local LWaibj[a,i,b,j] 
     local LWiaai[i1,a,a2,i]
     local LWjbai[j,b,a,i]
#
# Distributed two-particle arrays 
# ------------------------------- 
#
     distributed Wminj_aa[i2,i,i3,i1] 
     distributed Wminj_bb[j2,j,j3,j1] 
     distributed Wminj_ab[i2,i,j3,j1] 
#
# Declare scalars 
# --------------- 
#
      scalar ccsd_correlation
      scalar ccsd_energy
      scalar etemp
      scalar esum
      scalar esuma
      scalar esumb
      scalar esumab
      scalar ecorraa
      scalar ecorrbb
      scalar ecorrab
      scalar ecorrT
      scalar enew 
      scalar eold 
      scalar ecrit 
      scalar ediff 
      scalar mp2_energy
#
# Arrays/Scalars used exclusively in DIIS procedure. 
# -------------------------------------------------- 
#
       served e1aibj[a,i,b,j]  
       served e2aibj[a,i,b,j]  
       served e3aibj[a,i,b,j]  
       served e4aibj[a,i,b,j]  
       served e5aibj[a,i,b,j]  
       served e6aibj[a,i,b,j]  
#
       served e5aiai[a,i,b,j] 
#
#    ------------------------------------------------------------------------ 
#
     PROC READ_2EL   
# 
#    ------------------------------------------------------------------------ 
#
      restore_persistent ca    "ca" 
      set_persistent     ca    "ca" 
      restore_persistent Fock_a "fock_a" 
      set_persistent     Fock_a "fock_a"
      restore_persistent VSpipi "VSpipi"
      restore_persistent Vaaii  "Vaaii"
      restore_persistent Viaai  "Viaai"
      restore_persistent Vaaai  "Vaaai"
      restore_persistent Vpiqj  "Vpiqj"
     #restore_persistent energy  "energy"

      server_barrier
#
#    ------------------------------------------------------------------------ 
#
     ENDPROC READ_2EL   
# 
#    ------------------------------------------------------------------------ 
#
#    ------------------------------------------------------------------------ 
#
#    ------------------------------------------------------------------------ 
#
     PROC IGUESS_UHF  
# 
#    ------------------------------------------------------------------------ 
#
     PARDO a, b, i, j 
#
           REQUEST                     Vpiqj[a,i,b,j]   
           REQUEST                     Vpiqj[b,j,a,i]   
           Taibj[a,i,b,j]            = Vpiqj[b,j,a,i]
           Taibj[a,i,b,j]           += Vpiqj[a,i,b,j]
           execute energy_denominator_rhf  Taibj[a,i,b,j] fock_a  
           Taibj[a,i,b,j]           *= 0.5  
           PREPARE T2old_ab[a,i,b,j] = Taibj[a,i,b,j]  
          #DO kdiis 
          #   if kdiis == 1 
                 PREPARE D0aibj[a,i,b,j]  = Taibj[a,i,b,j]  
          #   endif 
          #ENDDO kdiis 
#
     ENDPARDO a, b, i, j 
#
     PARDO a, i
#
           tai[a,i]         = 0.0
           PUT t1a_old[a,i] = tai[a,i]
          #DO kdiis 
          #   if kdiis == 1 
                 PREPARE D0ai[a,i] = tai[a,i]
          #   endif 
          #ENDDO kdiis 
#
     ENDPARDO a, i
#
#    ------------------------------------------------------------------------ 
# 
     ENDPROC IGUESS_UHF 
# 
#    ------------------------------------------------------------------------ 
#
      PROC TAUAA 
#     ----------
#
      PARDO a, i, a1, i1 
#
            REQUEST T2old_aa[a,i,a1,i1]   
            GET t1a_old[a1,i1] 
            GET t1a_old[a1,i] 
            GET t1a_old[a,i1] 
            GET t1a_old[a,i] 
# 
            tai[a1,i1]                 = t1a_old[a1,i1] 
            tmp1_aiai[a,i,a1,i1]       = t1a_old[a,i]^tai[a1,i1]  
            tai[a1,i]                  = t1a_old[a1,i] 
            tmp2_aiai[a,i,a1,i1]       = t1a_old[a,i1]^tai[a1,i]  
            tmp1_aiai[a,i,a1,i1]      -= tmp2_aiai[a,i,a1,i1]  
#
            tmp1_aiai[a,i,a1,i1]      *= 0.5  
            tmp1_aiai[a,i,a1,i1]      += T2old_aa[a,i,a1,i1] 
            PREPARE Taup_aa[a,i,a1,i1] = tmp1_aiai[a,i,a1,i1]  
#
      ENDPARDO a, i, a1, i1 
#
      ENDPROC TAUAA 
#     ------------- 
# 
#    ------------------------------------------------------------------------ 
#
#    ------------------------------------------------------------------------ 
#
      PROC TAUAB 
#     ----------
#
      PARDO a, i, b, j 
#
            REQUEST T2old_ab[a,i,b,j]   
            GET t1a_old[a,i] 
            GET t1a_old[b,j] 
            tai[a,i] = t1a_old[a,i] 
#
            tmp1_aibj[a,i,b,j]       = tai[a,i]^t1a_old[b,j]  
            tmp2_aibj[a,i,b,j]       = tmp1_aibj[a,i,b,j]  
            tmp2_aibj[a,i,b,j]      *= 0.5  
#
            tmp1_aibj[a,i,b,j]      += T2old_ab[a,i,b,j] 
            tmp2_aibj[a,i,b,j]      += T2old_ab[a,i,b,j] 
            PREPARE Tau_ab[a,i,b,j]  = tmp1_aibj[a,i,b,j]  
            PREPARE Taup_ab[a,i,b,j] = tmp2_aibj[a,i,b,j]  
#
      ENDPARDO a, i, b, j 
#
      ENDPROC TAUAB 
#     ------------- 
#
#    ------------------------------------------------------------------------ 
#
#    ------------------------------------------------------------------------ 
#
      PROC TAUPAA 
#     -----------
#
      PARDO a, i, a1, i1 
#
            REQUEST T2old_aa[a,i,a1,i1]   
            GET t1a_old[a1,i1] 
            GET t1a_old[a1,i] 
            GET t1a_old[a,i1] 
            GET t1a_old[a,i] 
#
            tai[a1,i1]                 = t1a_old[a1,i1] 
            tmp1_aiai[a,i,a1,i1]       = t1a_old[a,i]^tai[a1,i1]  
            tai[a1,i]                  = t1a_old[a1,i] 
            tmp2_aiai[a,i,a1,i1]       = t1a_old[a,i1]^tai[a1,i]  
            tmp1_aiai[a,i,a1,i1]      -= tmp2_aiai[a,i,a1,i1] 
            tmp1_aiai[a,i,a1,i1]      *= 0.5  
            tmp1_aiai[a,i,a1,i1]      += T2old_aa[a,i,a1,i1] 
            PREPARE Taup_aa[a,i,a1,i1] = tmp1_aiai[a,i,a1,i1]  
#
      ENDPARDO a, i, a1, i1 
#
      ENDPROC TAUPAA 
#
# PPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPP 
#
# PPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPP 
#
      PROC TAUPAB 
#     -----------
#
      PARDO a, i, b, j 
#
            REQUEST T2old_ab[a,i,b,j]   
            GET t1a_old[a,i] 
            GET t1a_old[b,j] 
            tai[a,i] = t1a_old[a,i] 
# 
            tmp1_aibj[a,i,b,j]       = tai[a,i]^t1a_old[b,j]  
            tmp1_aibj[a,i,b,j]      *= 0.5  
            tmp1_aibj[a,i,b,j]      += T2old_ab[a,i,b,j] 
            PREPARE Taup_ab[a,i,b,j] = tmp1_aibj[a,i,b,j]  
#
      ENDPARDO a, i, b, j 
#
      ENDPROC TAUPAB 
#
# PPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPP 
#
# PPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPP 
#
      PROC TAU 
#
           CALL TAUAA 
           CALL TAUAB 
#
      ENDPROC TAU 
#
      PROC TAUP
#
           CALL TAUPAA 
           CALL TAUPAB 
#
      ENDPROC TAUP
#
#    ------------------------------------------------------------------------ 
#
#    ------------------------------------------------------------------------ 
#
      PROC FAEA
#     ----------
#
      PARDO a, a2, i
#     
               allocate LWiaai[*,a,a2,i]
#     
               DO a1
#     
                  REQUEST            Vaaai[a1,a,a2,i] 
                  REQUEST            Vaaai[a2,a,a1,i] 
                  GET                t1a_old[a2,i]
#     
                  taaai[a1,a,a2,i] = Vaaai[a1,a,a2,i]
                  t1aaai[a1,a,a2,i]= Vaaai[a2,a,a1,i]
                  taaai[a1,a,a2,i]-= t1aaai[a1,a,a2,i]
#     
                  Taa[a,a1]        = taaai[a1,a,a2,i]*t1a_old[a2,i]
                  PUT Fae_a[a,a1] += Taa[a,a1]
#     
                  DO i1
#
                     GET                  t1a_old[a1,i1]
                     tiaai[i1,a,a2,i]   = taaai[a1,a,a2,i]*t1a_old[a1,i1]
                     LWiaai[i1,a,a2,i] += tiaai[i1,a,a2,i]
#
                  ENDDO i1
#
               ENDDO a1
#
               DO i1
#
                  Taiai[a2,i,a,i1]          = LWiaai[i1,a,a2,i]
                  PREPARE e5aiai[a2,i,a,i1] = Taiai[a2,i,a,i1]
#
               ENDDO i1
#
               deallocate LWiaai[*,a,a2,i]
#
      ENDPARDO a, a2, i
#
      PARDO a, b, j
#
            allocate LWaibj[a,*,b,j]
#
            DO a1
#
               REQUEST            Vaaai[a1,a,b,j] 
               GET                t1a_old[b,j]
#
               DO i
#
                  GET                t1a_old[a1,i]
                  taibj[a,i,b,j]   = Vaaai[a1,a,b,j]*t1a_old[a1,i]
                  LWaibj[a,i,b,j] += taibj[a,i,b,j]
#
               ENDDO i
#
               Taa[a,a1]        = Vaaai[a1,a,b,j]*t1a_old[b,j]
               PUT Fae_a[a,a1] += Taa[a,a1]
#
            ENDDO a1
#
            DO i
#
               Taibj[a,i,b,j]          = LWaibj[a,i,b,j]
               T1aibj[b,j,a,i]         = LWaibj[a,i,b,j]
               PREPARE e6aibj[a,i,b,j] = Taibj[a,i,b,j]
               PREPARE e5aibj[b,j,a,i] = T1aibj[b,j,a,i] 
#
            ENDDO i
#
            deallocate LWaibj[a,*,b,j]
#
      ENDPARDO a, b, j
#
      PARDO a1, i, b, j
#
           REQUEST Vpiqj[a1,i,b,j] 
#
           DO a
#
              REQUEST            Taup_ab[a,i,b,j] 
#
              Taa[a,a1]        = Taup_ab[a,i,b,j]*Vpiqj[a1,i,b,j]
              Taa[a,a1]       *= -1.0
#
              PUT Fae_a[a,a1] += Taa[a,a1]
#
           ENDDO a
#
      ENDPARDO a1, i, b, j
#
      PARDO a1, i, a2, i1
#
           REQUEST VSpipi[a1,i1,a2,i] 
#
           DO a
#
               REQUEST Taup_aa[a,i,a2,i1] 
#
               Taa[a,a1]        = Taup_aa[a,i,a2,i1]*VSpipi[a1,i1,a2,i]
               Taa[a,a1]       *= 0.5
#
               PUT Fae_a[a,a1] += Taa[a,a1]
#
            ENDDO a
#
      ENDPARDO a1, i, a2, i1
#
      ENDPROC FAEA
#     -------------
#
#    ------------------------------------------------------------------------ 
#
#    ------------------------------------------------------------------------ 
#
      PROC FAEB
#     ----------
#
      PARDO a, i, b
#
            allocate LWjbai[*,b,a,i]
#              
            DO b1
#           
               REQUEST Vaaai[b1,b,a,i] 
               GET     t1a_old[a,i] 
#
               DO j  
#
                  GET                t1a_old[b1,j]
                  tjbai[j,b,a,i]   = Vaaai[b1,b,a,i]*t1a_old[b1,j]
                  LWjbai[j,b,a,i] += tjbai[j,b,a,i]
#    
               ENDDO j
#    
            ENDDO b1
#     
            DO j
#        
               Taibj[a,i,b,j]          = LWjbai[j,b,a,i]
               PREPARE e5aibj[a,i,b,j] = Taibj[a,i,b,j]
#     
            ENDDO j
#    
            deallocate LWjbai[*,b,a,i]
#    
      ENDPARDO a, i, b
#
      ENDPROC FAEB
#     -------------
#
#    ------------------------------------------------------------------------ 
#
#    ------------------------------------------------------------------------ 
#
      PROC FAE
#     --------
         CALL FAEA 
        #CALL FAEB 
      ENDPROC FAE
#     ----------- 
#
#    ------------------------------------------------------------------------ 
#
#    ------------------------------------------------------------------------ 
#
      PROC FMEA
#     ----------
#
      PARDO i, a 
#
            Tme_a[i,a] = 0.0
# 
            DO i1 
            DO a1 
#
               REQUEST VSpipi[a,i,a1,i1]   
               GET     t1a_old[a1,i1] 
#
               Tia[i,a]    = VSpipi[a,i,a1,i1]*t1a_old[a1,i1] 
               Tme_a[i,a] += Tia[i,a] 
#
            ENDDO a1 
            ENDDO i1 
# 
            DO j 
            DO b 
#
               REQUEST Vpiqj[a,i,b,j]   
               GET     t1a_old[b,j] 
#
               Tia[i,a]    = Vpiqj[a,i,b,j]*t1a_old[b,j] 
               Tme_a[i,a] += Tia[i,a] 
#
            ENDDO b 
            ENDDO j 
#
            PUT Fme_a[i,a] = Tme_a[i,a] 
#
      ENDPARDO i, a 
# 
      ENDPROC FMEA
#     -------------
#
#    ------------------------------------------------------------------------ 
#
#    ------------------------------------------------------------------------ 
#
      PROC FMIA
#     ----------
#
      PARDO i1, i, a, i2
#
            REQUEST VSpipi[a,i2,i,i1]   
            GET t1a_old[a,i2] 
#
            Tii[i1,i]        = VSpipi[a,i2,i,i1]*t1a_old[a,i2] 
            PUT Fmi_a[i1,i] += Tii[i1,i] 
#
      ENDPARDO i1, i, a, i2  
#
      PARDO a, a2, i2  
#
            allocate L3aiai[a2,*,a,i2] 
#
            DO i1 
#
               REQUEST VSpipi[a2,i1,a,i2]  
               L3aiai[a2,i1,a,i2] = VSpipi[a2,i1,a,i2] 
#
            ENDDO i1 
#
            DO i   
#
               REQUEST Taup_aa[a2,i,a,i2]  
# 
               DO i1    
#
                  Tii[i1,i]        = L3aiai[a2,i1,a,i2]*Taup_aa[a2,i,a,i2] 
                  Tii[i1,i]       *= 0.5 
                  PUT Fmi_a[i1,i] += Tii[i1,i] 
#
               ENDDO i1  
#
            ENDDO i   
#
            deallocate L3aiai[a2,*,a,i2] 
#
      ENDPARDO a, a2, i2  
#
      PARDO i1, i, b, j  
#
            REQUEST Vpiqj[i,i1,b,j]   
            GET t1a_old[b,j] 
#
            Tii[i1,i]        = Vpiqj[i,i1,b,j]*t1a_old[b,j] 
            PUT Fmi_a[i1,i] += Tii[i1,i] 
#
      ENDPARDO i1, i, b, j  
#
      PARDO i1, i, b, j  
#
            DO a 
#
               REQUEST Vpiqj[a,i1,b,j]    
               REQUEST Taup_ab[a,i,b,j]  
 
               Tii[i1,i]        = Taup_ab[a,i,b,j]*Vpiqj[a,i1,b,j] 
               PUT Fmi_a[i1,i] += Tii[i1,i] 
#
            ENDDO a 
#
      ENDPARDO i1, i, b, j  
#
      ENDPROC FMIA
#     -------------
#
#    ------------------------------------------------------------------------ 
#
#    ------------------------------------------------------------------------ 
#
# In the procedure T1ANEW all contributions to the singles amplitude t1a 
# are computed. 
#
      PROC T1ANEW 
#     -----------
#
      PARDO a, i
#
            tai[a,i] = 0.0
#
            DO i1
#
               GET t1a_old[a,i1]
               GET Fmi_a[i1,i]
#
               t1ai[a,i] = t1a_old[a,i1]*Fmi_a[i1,i]
               tai[a,i] -= t1ai[a,i]
#
            ENDDO i1
#
            DO b
            DO j
#
               REQUEST T2old_ab[a,i,b,j] 
               REQUEST Viaai[i,a,b,j] 
               GET Fme_a[j,b]
               GET t1a_old[b,j]
#
               t1ai[a,i] = T2old_ab[a,i,b,j]*Fme_a[j,b]
               tai[a,i] += t1ai[a,i]
#
               t2ai[a,i]  = Viaai[i,a,b,j]*t1a_old[b,j]
               tai[a,i] += t2ai[a,i]
#
            ENDDO j
            ENDDO b
#
            DO a1
#
               GET t1a_old[a1,i]
               GET Fae_a[a,a1]
#
               t1ai[a,i] = t1a_old[a1,i]*Fae_a[a,a1]
               tai[a,i] += t1ai[a,i]
#
               DO i1
#
                  REQUEST T2old_aa[a,i,a1,i1] 
                  REQUEST Vaaii[a,a1,i1,i] 
                  REQUEST Viaai[i1,a1,a,i] 
                  GET Fme_a[i1,a1]
                  GET t1a_old[a1,i1]
#
                  t1ai[a,i] = T2old_aa[a,i,a1,i1]*Fme_a[i1,a1]
                  tai[a,i] += t1ai[a,i]
#
                  Tpppp[i1,a1,a,i]  = Vaaii[a,a1,i1,i]
                  Tpppp[i1,a1,a,i] -= Viaai[i1,a1,a,i]
#
                  t2ai[a,i] = Tpppp[i1,a1,a,i]*t1a_old[a1,i1]
                  tai[a,i] -= t2ai[a,i]
#
               ENDDO i1
#
            ENDDO a1
#
            PUT t1a_new[a,i] += tai[a,i]
#
      ENDPARDO a, i
#
      PARDO a1, a2, i1
#
            allocate L4aiai[a1,*,a2,i1]
#
            DO i
#
               REQUEST T2old_aa[a1,i,a2,i1] 
               L4aiai[a1,i,a2,i1] = T2old_aa[a1,i,a2,i1]
#
            ENDDO i
#
            DO a
#
               REQUEST Vaaai[a2,a,a1,i1]   
               REQUEST Vaaai[a1,a,a2,i1]   
               taaai[a2,a,a1,i1] = Vaaai[a2,a,a1,i1] 
               t1aaai[a2,a,a1,i1]= Vaaai[a1,a,a2,i1] 
               taaai[a2,a,a1,i1]-= t1aaai[a2,a,a1,i1] 
#
               DO i
#
                  tai[a,i]  = taaai[a2,a,a1,i1]*L4aiai[a1,i,a2,i1]
                  tai[a,i] *= -0.5
#
                  PUT t1a_new[a,i] += tai[a,i]
#
               ENDDO i
#
            ENDDO a
#
            deallocate L4aiai[a1,*,a2,i1]
#
      ENDPARDO a1, a2, i1
#
      PARDO a1, i1, i2
#
            allocate L1aiii[a1,i2,*,i1]
#
            DO i
#
               REQUEST VSpipi[a1,i2,i,i1] 
               L1aiii[a1,i2,i,i1] = VSpipi[a1,i2,i,i1]
#
            ENDDO i
#
            DO a
#
               REQUEST T2old_aa[a,i1,a1,i2] 
#
               DO i
#
                  tai[a,i]  = L1aiii[a1,i2,i,i1]*T2old_aa[a,i1,a1,i2]
                  tai[a,i] *= -0.5
#
                  PUT t1a_new[a,i] += tai[a,i]
#
               ENDDO i
#
            ENDDO a
#
            deallocate L1aiii[a1,i2,*,i1]
#
      ENDPARDO a1, i1, i2
#
      PARDO a1, b, j
#
            allocate L1aibj[a1,*,b,j]
#
            DO i
#
               REQUEST T2old_ab[a1,i,b,j] 
               L1aibj[a1,i,b,j] = T2old_ab[a1,i,b,j]
#
            ENDDO i
#
            DO a
#
               REQUEST Vaaai[a1,a,b,j] 
#
               DO i
#
                  tai[a,i]  = Vaaai[a1,a,b,j]*L1aibj[a1,i,b,j]
#
                  PUT t1a_new[a,i] += tai[a,i]
#
               ENDDO i
#
            ENDDO a
#
            deallocate L1aibj[a1,*,b,j]
#
      ENDPARDO a1, b, j
#
      PARDO b, j, i1
#
            allocate L1iibj[*,i1,b,j]
#
            DO i
#
               REQUEST Vpiqj[i,i1,b,j] 
               L1iibj[i,i1,b,j] = Vpiqj[i,i1,b,j]
#
            ENDDO i
#
            DO a
#
               REQUEST T2old_ab[a,i1,b,j] 
#
               DO i
#
                  tai[a,i]  = L1iibj[i,i1,b,j]*T2old_ab[a,i1,b,j]
                  tai[a,i] *= -1.0
#
                  PUT t1a_new[a,i] += tai[a,i]
#
               ENDDO i
#
            ENDDO a
#
            deallocate L1iibj[*,i1,b,j]
#
      ENDPARDO b, j, i1
#
      ENDPROC T1ANEW 
#     --------------
#
# ---------------------------------------------------------------------------- 
#
# ---------------------------------------------------------------------------- 
#
      PROC WminjAB
#     ------------
#
      create Wminj_ab
      sip_barrier
      PARDO i, i1, j, j1
#
            REQUEST                    Vpiqj[i1,i,j1,j] 
            Tiijj[i1,i,j1,j]         = Vpiqj[i1,i,j1,j]
            PUT Wminj_ab[i1,i,j1,j] += Tiijj[i1,i,j1,j]
#
      ENDPARDO i, i1, j, j1
#
      PARDO i,i1,b,j1
#
         REQUEST Vpiqj[i,i1,b,j1] 

         DO j
#
            GET                        t1a_old[b,j]
            Tiijj[i1,i,j1,j]         = Vpiqj[i,i1,b,j1]*t1a_old[b,j]
            T1iijj[j1,j,i1,i]        = Tiijj[i1,i,j1,j]  
            PUT Wminj_ab[i1,i,j1,j] += Tiijj[i1,i,j1,j]
            PUT Wminj_ab[j1,j,i1,i] += T1iijj[j1,j,i1,i]
#
         ENDDO j
#
      ENDPARDO i,i1,b,j1
#
      PARDO i1, j1, a, b
#
         REQUEST Vpiqj[a,i1,b,j1] 
#
         DO j
         DO i
#
            REQUEST                    Tau_ab[a,i,b,j]  
            Tiijj[i1,i,j1,j]         = Tau_ab[a,i,b,j]*Vpiqj[a,i1,b,j1]
            Tiijj[i1,i,j1,j]        *= 0.5
            PUT Wminj_ab[i1,i,j1,j] += Tiijj[i1,i,j1,j]
#
         ENDDO i
         ENDDO j
#
      ENDPARDO i1, j1, a, b
      sip_barrier
#
      PARDO i1, j1, a, b
#
            REQUEST Tau_ab[a,i1,b,j1] 
#
            DO i
            DO j
#
               GET                          Wminj_ab[i1,i,j1,j]
               T1aibj[a,i,b,j]            = Tau_ab[a,i1,b,j1]*Wminj_ab[i1,i,j1,j]
               PREPARE T2new_ab[a,i,b,j] += T1aibj[a,i,b,j]
#
            ENDDO j
            ENDDO i
#
      ENDPARDO i1, j1, a, b
      sip_barrier
      delete Wminj_ab
#
      ENDPROC WminjAB
#     --------------- 
# 
# PPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPP 
#
      PROC WMINJ 
#
           CALL WminjAB 
#
      ENDPROC WMINJ 
# 
# ----------------------------------------------------------------------------
#
# PPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPP 
#
# PPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPP 
#
# In PROCEDURE T2NEWAB the new amplitudes T2new_ab are computed. Contributions 
# involving large two-particle intermediates are computed in the corresponding  
# procedures where these intermediates(temp) are computed. Thus the 
# intermediate Wminj is stored and its contribution computed here.  
#
# Contributions coming from P_(ij)P_(ab)t^e_i t^a_m <mb||ej> are computed 
# elsewhere. 
#
      PROC T2NEWAB    
#     ------------
#
      PARDO i, b, j 
#
            allocate Liibj[*,i,b,j] 
            allocate L1aibj[*,i,b,j]   
#
            DO i1 
#
               REQUEST           Vpiqj[i1,i,b,j]  
               Liibj[i1,i,b,j] = Vpiqj[i1,i,b,j] 
#
            ENDDO i1 
#
            DO a1 
#
               REQUEST            T2old_ab[a1,i,b,j]  
               L1aibj[a1,i,b,j] = T2old_ab[a1,i,b,j] 
#
            ENDDO a1 
#
            DO a 
#
               REQUEST          Vpiqj[a,i,b,j]  
               Taibj[a,i,b,j] = Vpiqj[a,i,b,j]   
               Taibj[a,i,b,j]*= 0.5  
#
               DO i1 
#
                  GET t1a_old[a,i1] 
#
                  T1aibj[a,i,b,j] = Liibj[i1,i,b,j]*t1a_old[a,i1] 
                  Taibj[a,i,b,j] -= T1aibj[a,i,b,j] 
#
               ENDDO i1 
#
               DO a1 
#
                  GET Fae_a[a,a1] 
                  tmp_aa[a,a1] = Fae_a[a,a1]    
#
                  DO i1 
#
                     GET t1a_old[a,i1] 
                     GET Fme_a[i1,a1] 
                     tmp1_aa[a,a1]  = t1a_old[a,i1]*Fme_a[i1,a1] 
                     tmp1_aa[a,a1] *= 0.5 
                     tmp_aa[a,a1]  -= tmp1_aa[a,a1]  
#
                  ENDDO i1 
#
                  T1aibj[a,i,b,j] = L1aibj[a1,i,b,j]*tmp_aa[a,a1]  
                  Taibj[a,i,b,j] += T1aibj[a,i,b,j] 
#
               ENDDO a1 
#
               T1aibj[b,j,a,i]            = Taibj[a,i,b,j]  
               PREPARE T2NEW_AB[a,i,b,j] += Taibj[a,i,b,j]  
               PREPARE T2NEW_AB[b,j,a,i] += T1aibj[b,j,a,i]  
#
            ENDDO a 
#
            deallocate Liibj[*,i,b,j] 
            deallocate L1aibj[*,i,b,j]   
#
      ENDPARDO i, b, j 
#
      PARDO a, b, j 
#
            allocate L3aibj[a,*,b,j] 
            allocate Laabj[a,*,b,j] 
#
            DO i1 
#
               REQUEST T2old_ab[a,i1,b,j]  
               L3aibj[a,i1,b,j] = T2old_ab[a,i1,b,j] 
#
            ENDDO i1 
#
            DO a1 
#
               REQUEST Vaaai[a,a1,b,j]   
               Laabj[a,a1,b,j] = Vaaai[a,a1,b,j]  
#
            ENDDO a1 
#
            DO i 
#
               Taibj[a,i,b,j] = 0.0  
#
               DO i1 
#
                  GET Fmi_a[i1,i] 
                  tmp_ii[i1,i] = Fmi_a[i1,i] 
#
                  DO a1 
#
                     GET t1a_old[a1,i] 
                     GET Fme_a[i1,a1] 
#
                     tmp1_ii[i1,i]  = t1a_old[a1,i]*Fme_a[i1,a1] 
                     tmp1_ii[i1,i] *= 0.5 
                     tmp_ii[i1,i]  += tmp1_ii[i1,i] 
#
                  ENDDO a1  
#
                  T1aibj[a,i,b,j] = L3aibj[a,i1,b,j]*tmp_ii[i1,i] 
                  Taibj[a,i,b,j] -= T1aibj[a,i,b,j] 
#
               ENDDO i1 
#
               DO a1 
#
                  GET t1a_old[a1,i] 
#
                  T1aibj[a,i,b,j] = Laabj[a,a1,b,j]*t1a_old[a1,i]  
                  Taibj[a,i,b,j] += T1aibj[a,i,b,j] 
#
               ENDDO a1 
#
               T1aibj[b,j,a,i]            = Taibj[a,i,b,j]  
               PREPARE T2NEW_AB[a,i,b,j] += Taibj[a,i,b,j]  
               PREPARE T2NEW_AB[b,j,a,i] += T1aibj[b,j,a,i]  
#
            ENDDO i 
#
            deallocate L3aibj[a,*,b,j] 
            deallocate Laabj[a,*,b,j] 
#
      ENDPARDO a, b, j 
#
      ENDPROC T2NEWAB    
#     ---------------
#
#    ------------------------------------------------------------------------ 
#
     PROC AOLADDER_GPU   
#
#    ------------------------------------------------------------------------ 
#
#    First create and zero-out intermediate arrays
#    ---------------------------------------------
#
     PARDO mu, nu, i, j
           Txixj[mu,i,nu,j]           = 0.0
           PREPARE TAO_ab[mu,i,nu,j]  = Txixj[mu,i,nu,j]
           PREPARE T2AO_ab[mu,i,nu,j] = Txixj[mu,i,nu,j]
     ENDPARDO mu, nu, i, j
#
     server_barrier 
#
#    Form Half back transformed cluster arrays  
#    -----------------------------------------
#
#    AB spin combination
#     -------------------
#
     PARDO b, a, j, i
#
           REQUEST Tau_ab[a,i,b,j] 
#
           DO mu
#
              Taixj[a,i,mu,j] = Tau_ab[a,i,b,j]*ca[mu,b]
#
              DO nu
#
                 Txixj[nu,i,mu,j]           = Taixj[a,i,mu,j]*ca[nu,a]
                 PREPARE TAO_ab[nu,i,mu,j] += Txixj[nu,i,mu,j]
#
              ENDDO nu
#
           ENDDO mu
#
      ENDPARDO b, a, j, i
#
     create t1a_ax 
     server_barrier   
#
#    Contract AOINT with half back transformed Amplitudes 
#    ----------------------------------------------------    
#
     PARDO lambda, sigma
        allocate LTAO_ab[lambda,*,sigma,*] 
        DO i
        DO j
           REQUEST                     TAO_ab[lambda,i,sigma,j] 
           LTAO_ab[lambda,i,sigma,j] = TAO_ab[lambda,i,sigma,j]  
        ENDDO j
        ENDDO i

        DO mu 
        DO nu
        WHERE mu < nu 
           execute compute_integral_batch aoint[lambda,mu,sigma,nu] 
           DO i
           DO j
              Yab[mu,i,nu,j]              = 0.0  
              execute aoladder_contraction aoint[lambda,mu,sigma,nu] LTAO_ab[lambda,i,sigma,j] Yab[mu,i,nu,j] 
              Y1ab[nu,j,mu,i]             = Yab[mu,i,nu,j]
#
              PREPARE T2AO_ab[mu,i,nu,j] += Yab[mu,i,nu,j]
              PREPARE T2AO_ab[nu,j,mu,i] += Y1ab[nu,j,mu,i]
#
           ENDDO j
           ENDDO i
#
        ENDDO nu
        ENDDO mu 
        deallocate LTAO_ab[lambda,*,sigma,*] 
     ENDPARDO lambda, sigma
#
     PARDO lambda, sigma
        allocate LTAO_ab[lambda,*,sigma,*] 
        DO i
        DO j
           REQUEST                     TAO_ab[lambda,i,sigma,j] 
           LTAO_ab[lambda,i,sigma,j] = TAO_ab[lambda,i,sigma,j]  
        ENDDO j
        ENDDO i
        DO mu 
        DO nu
        WHERE mu == nu 
#
           execute compute_integral_batch aoint[lambda,mu,sigma,nu] 
#
           DO i
           DO j
              Yab[mu,i,nu,j]              = 0.0  
              execute aoladder_contraction aoint[lambda,mu,sigma,nu] LTAO_ab[lambda,i,sigma,j] Yab[mu,i,nu,j] 
              PREPARE T2AO_ab[mu,i,nu,j] += Yab[mu,i,nu,j]
           ENDDO j
           ENDDO i
#
        ENDDO nu
        ENDDO mu 
        deallocate LTAO_ab[lambda,*,sigma,*] 
     ENDPARDO lambda, sigma
#
#    Half transform the t1 arrays. 
#    ----------------------------- 
#
     PARDO a, mu
#
           tpx[a,mu] = 0.0 
#
           DO i  
#
              GET          t1a_old[a,i] 
              t1px[a,mu] = t1a_old[a,i]*ca[mu,i] 
              tpx[a,mu] += t1px[a,mu] 
#
           ENDDO i  
#
           PUT t1a_ax[a,mu] = tpx[a,mu] 
#
     ENDPARDO a, mu
#
     server_barrier  
#
#    Perform final transformation
#    ----------------------------
#
        PARDO nu, i, j
#
              allocate LDaixj[*,i,nu,j]
#
              DO mu
#
                 REQUEST T2AO_ab[mu,i,nu,j] 
#
                 DO a
                    Taixj[a,i,nu,j]   = T2AO_ab[mu,i,nu,j]*ca[mu,a]
                    LDaixj[a,i,nu,j] += Taixj[a,i,nu,j]
                 ENDDO a
#
              ENDDO mu
#
              DO a
                 PREPARE TDaixj[a,i,nu,j] = LDaixj[a,i,nu,j]
              ENDDO a
#
              deallocate LDaixj[*,i,nu,j]
#
        ENDPARDO nu, i, j
        server_barrier
#
        PARDO a, i, j, nu
#
              REQUEST TDaixj[a,i,nu,j] 
#
              DO b
#
                 GET                          t1a_ax[b,nu]
#
                 Taibj[a,i,b,j]             = TDaixj[a,i,nu,j]*ca[nu,b]
                 T1aibj[a,i,b,j]            = TDaixj[a,i,nu,j]*t1a_ax[b,nu]
                 T1aibj[a,i,b,j]           *= -1.0
                 T2aibj[b,j,a,i]            = T1aibj[a,i,b,j]
#
                 PREPARE T2new_ab[a,i,b,j] += Taibj[a,i,b,j]
                 PREPARE T2new_ab[a,i,b,j] += T1aibj[a,i,b,j]
                 PREPARE T2new_ab[b,j,a,i] += T2aibj[b,j,a,i]
#
              ENDDO b
#
        ENDPARDO a, i, j, nu
#
        sip_barrier
#
     delete t1a_ax 
#
#    ------------------------------------------------------------------------ 
#
     ENDPROC AOLADDER_GPU   
#
#    ------------------------------------------------------------------------ 
#
#
# PPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPP 
#
#    ------------------------------------------------------------------------ 
#

     PROC AOLADDER  
#
#    ------------------------------------------------------------------------ 
#
#    First create and zero-out intermediate arrays
#    ---------------------------------------------
#
     PARDO mu, nu, i, j
           Txixj[mu,i,nu,j]           = 0.0
           PREPARE TAO_ab[mu,i,nu,j]  = Txixj[mu,i,nu,j]
           PREPARE T2AO_ab[mu,i,nu,j] = Txixj[mu,i,nu,j]
     ENDPARDO mu, nu, i, j
#
     server_barrier 
#
#    Form Half back transformed cluster arrays  
#    -----------------------------------------
#
#    AB spin combination
#     -------------------
#
     PARDO b, a, j, i
#
           REQUEST Tau_ab[a,i,b,j] 
#
           DO mu
#
              Taixj[a,i,mu,j] = Tau_ab[a,i,b,j]*ca[mu,b]
#
              DO nu
#
                 Txixj[nu,i,mu,j]           = Taixj[a,i,mu,j]*ca[nu,a]
                 PREPARE TAO_ab[nu,i,mu,j] += Txixj[nu,i,mu,j]
#
              ENDDO nu
#
           ENDDO mu
#
      ENDPARDO b, a, j, i
#
     create t1a_ax 
     server_barrier   
#
#    Contract AOINT with half back transformed Amplitudes 
#    ----------------------------------------------------    
#
     PARDO mu, nu, lambda, sigma
        WHERE mu < nu 
#
           execute compute_integral_batch aoint[lambda,mu,sigma,nu] 
#
           DO i
           DO j
#
              REQUEST                       TAO_ab[lambda,i,sigma,j] 
              Yab[mu,i,nu,j]              = aoint[lambda,mu,sigma,nu]*TAO_ab[lambda,i,sigma,j]
              Y1ab[nu,j,mu,i]             = Yab[mu,i,nu,j]
#
              PREPARE T2AO_ab[mu,i,nu,j] += Yab[mu,i,nu,j]
              PREPARE T2AO_ab[nu,j,mu,i] += Y1ab[nu,j,mu,i]
#
           ENDDO j
           ENDDO i
#
     ENDPARDO mu, nu, lambda, sigma
#
     PARDO mu, nu, lambda, sigma
        WHERE mu == nu 
#
           execute compute_integral_batch aoint[lambda,mu,sigma,nu] 
#
           DO i
           DO j
#
              REQUEST                       TAO_ab[lambda,i,sigma,j] 
              Yab[mu,i,nu,j]              = aoint[lambda,mu,sigma,nu]*TAO_ab[lambda,i,sigma,j]
              PREPARE T2AO_ab[mu,i,nu,j] += Yab[mu,i,nu,j]
#
           ENDDO j
           ENDDO i
#
     ENDPARDO mu, nu, lambda, sigma
#
#    Half transform the t1 arrays. 
#    ----------------------------- 
#
     PARDO a, mu
#
           tpx[a,mu] = 0.0 
#
           DO i  
#
              GET          t1a_old[a,i] 
              t1px[a,mu] = t1a_old[a,i]*ca[mu,i] 
              tpx[a,mu] += t1px[a,mu] 
#
           ENDDO i  
#
           PUT t1a_ax[a,mu] = tpx[a,mu] 
#
     ENDPARDO a, mu
#
     server_barrier  
#
#    Perform final transformation
#    ----------------------------
#
        PARDO nu, i, j
#
              allocate LDaixj[*,i,nu,j]
#
              DO mu
#
                 REQUEST T2AO_ab[mu,i,nu,j] 
#
                 DO a
                    Taixj[a,i,nu,j]   = T2AO_ab[mu,i,nu,j]*ca[mu,a]
                    LDaixj[a,i,nu,j] += Taixj[a,i,nu,j]
                 ENDDO a
#
              ENDDO mu
#
              DO a
                 PREPARE TDaixj[a,i,nu,j] = LDaixj[a,i,nu,j]
              ENDDO a
#
              deallocate LDaixj[*,i,nu,j]
#
        ENDPARDO nu, i, j
        server_barrier
#
        PARDO a, i, j, nu
#
              REQUEST TDaixj[a,i,nu,j] 
#
              DO b
#
                 GET                          t1a_ax[b,nu]
#
                 Taibj[a,i,b,j]             = TDaixj[a,i,nu,j]*ca[nu,b]
                 T1aibj[a,i,b,j]            = TDaixj[a,i,nu,j]*t1a_ax[b,nu]
                 T1aibj[a,i,b,j]           *= -1.0
                 T2aibj[b,j,a,i]            = T1aibj[a,i,b,j]
#
                 PREPARE T2new_ab[a,i,b,j] += Taibj[a,i,b,j]
                 PREPARE T2new_ab[a,i,b,j] += T1aibj[a,i,b,j]
                 PREPARE T2new_ab[b,j,a,i] += T2aibj[b,j,a,i]
#
              ENDDO b
#
        ENDPARDO a, i, j, nu
#
        sip_barrier
#
     delete t1a_ax 
#
#    ------------------------------------------------------------------------ 
#
     ENDPROC AOLADDER  
#
#    ------------------------------------------------------------------------ 
#
#    ------------------------------------------------------------------------ 
#
# --------------------------------------------------------------------------- 
#
      PROC WAEBFAB 
#     ------------ 
#
# --------------------------------------------------------------------- 
# Comute contribution coming from:  
# 1/2 tau^{ab}_{mn} * [ 1/4 tau^{ef}_{ij} * (V^{mn}_{ef} - V^{mn}_{fe}]  
# --------------------------------------------------------------------- 
#
      create Wminj_ab 
      sip_barrier 
      PARDO i, j, a, b  
#
            REQUEST Tau_ab[a,i,b,j]    
#
            DO i1   
            DO j1  
#
               REQUEST                    Vpiqj[a,i1,b,j1]   
               Tiijj[i1,i,j1,j]         = Vpiqj[a,i1,b,j1]*Tau_ab[a,i,b,j] 
               PUT Wminj_ab[i1,i,j1,j] += Tiijj[i1,i,j1,j] 
#
            ENDDO j1  
            ENDDO i1   
#
      ENDPARDO i, j, a, b 
      sip_barrier 
#
      PARDO a, b, i1, j1  
#
            REQUEST Tau_ab[a,i1,b,j1]   
#
            DO i   
            DO j  
#
               GET                          Wminj_ab[i1,i,j1,j] 
               Taibj[a,i,b,j]             = Tau_ab[a,i1,b,j1]*Wminj_ab[i1,i,j1,j] 
               Taibj[a,i,b,j]            *= 0.5  
               PREPARE T2new_ab[a,i,b,j] += Taibj[a,i,b,j] 
#
            ENDDO j  
            ENDDO i  
#
      ENDPARDO a, b, i1, j1  
      sip_barrier 
      delete Wminj_ab 
#
      ENDPROC WAEBFAB 
#     --------------- 
#
# PPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPP
#
      PROC WAEBF 
# 
           CALL WAEBFAB 
#
      ENDPROC WAEBF 
#
# SSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSS
#
# SSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSS 
#
# In the procedures WMEBJAA, WMEBJBB, WMEBJAB, and WMEBJBA these  
# intermediates are formed as temporary arrarys. The contribution to the 
# outout arrays is computed directly from these temporary arrays so 
# that the array is never stored.   
#
# PPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPP 
#
      PROC WMEBJAA 
#     ------------
#
      PARDO a, a1, i, i1  
#
               REQUEST              Viaai[i1,a1,a,i]   
               REQUEST              Vaaii[a,a1,i1,i]   
               REQUEST              e5aiai[a1,i1,a,i]  
#
               TYaiai[a1,i1,a,i] = 0.0 
#
               DO i2
#
                  REQUEST              VSpipi[a1,i1,i,i2] 
                  GET                  t1a_old[a,i2]
#
                  T1aiai[a1,i1,a,i]  = VSpipi[a1,i1,i,i2]*t1a_old[a,i2]
                  TYaiai[a1,i1,a,i] -= T1aiai[a1,i1,a,i]
#
               ENDDO i2

               Taiai[a1,i1,a,i]           = Vaaii[a,a1,i1,i]
               TYaiai[a1,i1,a,i]         -= Taiai[a1,i1,a,i]
               Taiai[a1,i1,a,i]           = Viaai[i1,a1,a,i]
               TYaiai[a1,i1,a,i]         += Taiai[a1,i1,a,i]
               TYaiai[a1,i1,a,i]         += e5aiai[a1,i1,a,i]
               PREPARE TAUP_aa[a1,i1,a,i] = TYaiai[a1,i1,a,i] 
#
      ENDPARDO a, a1, i, i1  
      server_barrier 
#
      PARDO a, i, a2, i2
#
         REQUEST              T2old_aa[a2,i,a,i2]   
         GET                  t1a_old[a2,i] 
         GET                  t1a_old[a,i2] 
 
         tai[a,i2]          = t1a_old[a,i2] 
         T1aiai[a2,i2,a,i]  = t1a_old[a2,i]^tai[a,i2] 
         T2aiai[a2,i2,a,i]  = t2old_aa[a2,i,a,i2] 
         T2aiai[a2,i2,a,i] *= 0.5 
         T2aiai[a2,i2,a,i] += T1aiai[a2,i2,a,i] 

         DO i1
         DO a1 
#
            REQUEST                       VSpipi[a1,i1,a2,i2]   
            T3aiai[a1,i1,a,i]           = VSpipi[a1,i1,a2,i2]*T2aiai[a2,i2,a,i] 
            T3aiai[a1,i1,a,i]          *= -1.0  
            PREPARE TAUP_aa[a1,i1,a,i] += T3aiai[a1,i1,a,i]
#
         ENDDO a1
         ENDDO i1
#
      ENDPARDO a, i, a2, i2 

      PARDO a, i, b, j 
#
         REQUEST T2old_ab[a,i,b,j]  
#
         DO i1
         DO a1 
#
            REQUEST                       Vpiqj[a1,i1,b,j]  
            T1aiai[a1,i1,a,i]           = Vpiqj[a1,i1,b,j]*T2old_ab[a,i,b,j]
            T1aiai[a1,i1,a,i]          *= 0.5
            PREPARE TAUP_aa[a1,i1,a,i] += T1aiai[a1,i1,a,i]
#
         ENDDO a1
         ENDDO i1
#
      ENDPARDO a, i, b, j
      server_barrier
#
# Contributions to T2new_ab 
# ------------------------- 
#
      PARDO a, i, a1, i1 
#
            REQUEST TAUP_aa[a1,i1,a,i]   
#
            DO j  
            DO b
#
               REQUEST                      T2old_ab[a1,i1,b,j]   
               R1aibj[a,i,b,j]            = TAUP_aa[a1,i1,a,i]*T2old_ab[a1,i1,b,j] 
               Taibj[b,j,a,i]             = R1aibj[a,i,b,j] 
               PREPARE T2new_ab[a,i,b,j] += R1aibj[a,i,b,j]
               PREPARE T2new_ab[b,j,a,i] += Taibj[b,j,a,i]
#
            ENDDO b  
            ENDDO j  
#
      ENDPARDO a, i, a1, i1 
#
      ENDPROC WMEBJAA 
#     ---------------
#
# PPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPP 
#
# PPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPP 
#
      PROC WMEBJAB 
#     ------------
#
      PARDO j, b, i, a
#
               REQUEST           Viaai[i,a,b,j]   
               REQUEST           e5aibj[a,i,b,j]  
#
               Taibj[a,i,b,j] = 0.
#
               DO j1
#
                  REQUEST           Vpiqj[a,i,j,j1]   
                  GET               t1a_old[b,j1] 
                  T1aibj[a,i,b,j] = Vpiqj[a,i,j,j1]*t1a_old[b,j1] 
                  Taibj[a,i,b,j] -= T1aibj[a,i,b,j] 
#
               ENDDO j1

               T1aibj[a,i,b,j]          = Viaai[i,a,b,j] 
               Taibj[a,i,b,j]          += T1aibj[a,i,b,j]
               Taibj[a,i,b,j]          += e5aibj[a,i,b,j] 
               PREPARE TAUP_ab[a,i,b,j] = Taibj[a,i,b,j] 
#
      ENDPARDO j, b, i, a
      server_barrier

      PARDO j, b, j1, b1
#
               REQUEST              T2old_aa[b1,j,b,j1]  
               GET                  t1a_old[b1,j] 
               GET                  t1a_old[b,j1] 
# 
               tbj[b,j1]          = t1a_old[b,j1] 
               T2bjbj[b1,j,b,j1]  = t1a_old[b1,j]^tbj[b,j1] 
               T1bjbj[b1,j,b,j1]  = T2old_aa[b1,j,b,j1] 
               T1bjbj[b1,j,b,j1]  *= 0.5
               T1bjbj[b1,j,b,j1] += T2bjbj[b1,j,b,j1] 
#
               DO i  
               DO a 
#
                  REQUEST                     Vpiqj[a,i,b1,j1]   
                  Taibj[a,i,b,j]            = T1bjbj[b1,j,b,j1]*Vpiqj[a,i,b1,j1] 
                  Taibj[a,i,b,j]           *= -1.0  
                  PREPARE TAUP_ab[a,i,b,j] += Taibj[a,i,b,j]
#   
               ENDDO a 
               ENDDO i  
#
      ENDPARDO j, b, j1, b1
#
      PARDO b, j, a1, i1
#
            REQUEST T2old_ab[a1,i1,b,j]       
#
            DO a
            DO i
#
               REQUEST                     VSpipi[a1,i1,a,i] 
               T1aibj[a,i,b,j]           = T2old_ab[a1,i1,b,j]*VSpipi[a1,i1,a,i]
               T1aibj[a,i,b,j]          *= 0.5
               PREPARE TAUP_ab[a,i,b,j] += T1aibj[a,i,b,j]
#
            ENDDO i
            ENDDO a
#
      ENDPARDO b, j, a1, i1

      server_barrier 
#
# Contribution to T2new_ab 
# ------------------------ 
#
      PARDO i1, a1, a, i
#
            REQUEST T2old_aa[a1,i1,a,i]  
#
            DO j 
            DO b 
#
               REQUEST                        TAUP_ab[a,i,b,j]   
               R1aibj[a1,i1,b,j]            = T2old_aa[a1,i1,a,i]*TAUP_ab[a,i,b,j]  
               Taibj[b,j,a1,i1]             = R1aibj[a1,i1,b,j] 
               PREPARE T2new_ab[a1,i1,b,j] += R1aibj[a1,i1,b,j] 
               PREPARE T2new_ab[b,j,a1,i1] += Taibj[b,j,a1,i1] 
#
            ENDDO b 
            ENDDO j 
#
      ENDPARDO i1, a1, a, i
      server_barrier 
#
      ENDPROC WMEBJAB 
#     ---------------
#
# PPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPP 
#
# PPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPP 
#
# -------------------------------------------------------------------------------  
#
      PROC T2ABS2 
#     ----------- 
#
      PARDO i, j, i1, b  
#
           Tiibj[i1,i,b,j] = 0.0 
#
           DO a1 
# 
              REQUEST            Viaai[i1,a1,b,j]   
              GET                t1a_old[a1,i] 
#
              T1iibj[i1,i,b,j] = Viaai[i1,a1,b,j]*t1a_old[a1,i] 
              Tiibj[i1,i,b,j] += T1iibj[i1,i,b,j] 
#
           ENDDO a1  
#
           DO b1 
#
              REQUEST            Vaaii[b,b1,i1,i]   
              GET                t1a_old[b1,j] 
#
              T1iibj[i1,i,b,j] = Vaaii[b,b1,i1,i]*t1a_old[b1,j]  
              Tiibj[i1,i,b,j] += T1iibj[i1,i,b,j] 
#
          ENDDO b1  
#
          DO a 
#
             GET                          t1a_old[a,i1] 
             Taibj[a,i,b,j]             = Tiibj[i1,i,b,j]*t1a_old[a,i1] 
             Taibj[a,i,b,j]            *= -1.0 
             PREPARE T2new_ab[a,i,b,j] += Taibj[a,i,b,j] 
#
          ENDDO a 
#
      ENDPARDO i, j, i1, b  
#
      PARDO i, j, j1, a 
#
            Tjjai[j1,j,a,i] = 0.0 
#
            DO a1 
#
               REQUEST            Vaaii[a,a1,j1,j]   
               GET                t1a_old[a1,i] 
#
               T1jjai[j1,j,a,i] = Vaaii[a,a1,j1,j]*t1a_old[a1,i] 
               Tjjai[j1,j,a,i] += T1jjai[j1,j,a,i]
#
            ENDDO a1 
#
            DO b1 
# 
               REQUEST            Viaai[i,a,b1,j1]   
               GET                t1a_old[b1,j] 
#
               T1jjai[j1,j,a,i] = Viaai[i,a,b1,j1]*t1a_old[b1,j] 
               Tjjai[j1,j,a,i] += T1jjai[j1,j,a,i] 
#
            ENDDO b1  
#
            DO b 
#
               GET                          t1a_old[b,j1] 
               Taibj[a,i,b,j]             = Tjjai[j1,j,a,i]*t1a_old[b,j1] 
               Taibj[a,i,b,j]            *= -1.0 
               PREPARE T2new_ab[a,i,b,j] += Taibj[a,i,b,j] 
#
            ENDDO b 
#
      ENDPARDO i, j, j1, a  
#
      ENDPROC T2ABS2 
#     -------------- 
#
# PPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPP 
#
# Compute contributions from 'Wmjbe'. 
#
      PROC WMJBEab
#     -------------
#
      PARDO b, i, b1, i1
#
            REQUEST            Vaaii[b,b1,i1,i] 
            Tiiqq[i1,i,b,b1] = Vaaii[b,b1,i1,i]
#
            DO j1
#
               REQUEST             Vpiqj[i,i1,b1,j1] 
               GET                 t1a_old[b,j1]
#
               tbj[b,j1]         = t1a_old[b,j1]
               T1iiqq[i1,i,b,b1] = Vpiqj[i,i1,b1,j1]*tbj[b,j1]
               Tiiqq[i1,i,b,b1] -= T1iiqq[i1,i,b,b1]
#
            ENDDO j1
#
            PREPARE Wiibb[i1,i,b,b1] = Tiiqq[i1,i,b,b1]
#
      ENDPARDO b, i, b1, i1
      server_barrier

      PARDO a1, b, i1, b1
#
         REQUEST Vaaai[b1,b,a1,i1] 
#
         DO i
#
            GET                          t1a_old[a1,i]
            T1iiqq[i1,i,b,b1]          = Vaaai[b1,b,a1,i1]*t1a_old[a1,i]
            PREPARE Wiibb[i1,i,b,b1]  += T1iiqq[i1,i,b,b1]
#
         ENDDO i
#
      ENDPARDO a1, b, i1, b1

      PARDO i, b, a1, j1
#
         REQUEST     T2old_ab[a1,i,b,j1] 
         GET         t1a_old[a1,i]
         GET         t1a_old[b,j1]
         tai[a1,i] = t1a_old[a1,i] 

         Taibj[a1,i,b,j1]   = tai[a1,i]^t1a_old[b,j1]
         T1aibj[a1,i,b,j1]  = T2old_ab[a1,i,b,j1]
         T1aibj[a1,i,b,j1]  *= 0.5
         T1aibj[a1,i,b,j1] += Taibj[a1,i,b,j1]
#
         DO i1
         DO b1
#
            REQUEST                     Vpiqj[a1,i1,b1,j1] 
            Tiiqq[i1,i,b,b1]          = T1aibj[a1,i,b,j1]*Vpiqj[a1,i1,b1,j1]
            Tiiqq[i1,i,b,b1]         *= -1.0  
            PREPARE Wiibb[i1,i,b,b1] += Tiiqq[i1,i,b,b1]
#
         ENDDO b1
         ENDDO i1
#
      ENDPARDO i, b, a1, j1
      server_barrier

#  Calculate contribution to the amplitudes
#  ----------------------------------------
#
      PARDO a, j, i1, b1
#
            REQUEST T2old_ab[a,i1,b1,j] 
#
            DO i
            DO b
#
               REQUEST                      Wiibb[i1,i,b,b1] 
#
               Taibj[a,i,b,j]             = T2old_ab[a,i1,b1,j]*Wiibb[i1,i,b,b1]
               Taibj[a,i,b,j]            *= -1.0  
#
               T2aibj[b,j,a,i]            = Taibj[a,i,b,j] 
               PREPARE T2new_ab[a,i,b,j] += Taibj[a,i,b,j]
               PREPARE T2new_ab[b,j,a,i] += T2aibj[b,j,a,i]
#
            ENDDO b
            ENDDO i
#
      ENDPARDO a, j, i1, b1
#
      ENDPROC WMJBEab
#     ---------------
#
# SSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSS 
#
      PROC WMEBJ 
# 
           CALL WMEBJAA
           CALL WMEBJAB
           CALL WMJBEab
           CALL T2ABS2
#
      ENDPROC WMEBJ 
#
# SSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSS 
#
# PPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPP 
#
#    ------------------------------------------------------------------------ 
#
     PROC ENERGY_NEW 
# 
#    ------------------------------------------------------------------------ 
#
     ecorrab = 0.0 
     esumab  = 0.0 
     sip_barrier 
     PARDO a, b, i, j 
#
           REQUEST Vpiqj[a,i,b,j]   
           REQUEST Vpiqj[a,j,b,i]   
#
           REQUEST T2old_ab[a,i,b,j]   
           GET     t1a_old[a,i] 
           GET     t1a_old[b,j] 
           tai[a,i] = t1a_old[a,i] 
#
           Taibj[a,i,b,j]  = tai[a,i]^t1a_old[b,j] 
           Taibj[a,i,b,j] += T2old_ab[a,i,b,j]  
#
           T1aibj[a,i,b,j] = Vpiqj[a,i,b,j]  
           T2aibj[a,i,b,j] = Vpiqj[a,j,b,i]  
           T1aibj[a,i,b,j]*= 2.0  
           T1aibj[a,i,b,j]-= T2aibj[a,i,b,j]  
#
           etemp = Taibj[a,i,b,j]*T1aibj[a,i,b,j] 
           esumab += etemp 
#
     ENDPARDO a, b, i, j 

     sip_barrier 
#
     collective ecorrab += esumab
     ecorrT = ecorrab 
     print ecorrT  

#    ------------------------------------------------------------------------ 
# 
     ENDPROC ENERGY_NEW 
# 
#    ------------------------------------------------------------------------ 
# 
     PROC T2NEW_ZERO
#    --------------- 
#
#     Zero out new 1-particle amplitude arrays 
#     ---------------------------------------- 
#
          PARDO a, i 
                tai[a,i] = 0.0 
                PUT t1a_new[a,i] = tai[a,i] 
          ENDPARDO a, i 
#
#     Zero out new 2-particle amplitude arrays 
#     ---------------------------------------- 
#
          PARDO a, b, i, j 
                Taibj[a,i,b,j] = 0.0
                PREPARE T2new_ab[a,i,b,j] = Taibj[a,i,b,j] 
          ENDPARDO a, b, i, j 
# 
     ENDPROC T2NEW_ZERO
# 
#    ------------------------------------------------------------------------ 
#
# PPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPP 
#
      PROC UPDATET2 
#     ------------- 
#
      PARDO b, a, j, i
#
            REQUEST                    T2old_ab[b,j,a,i]  
            REQUEST                    T2new_ab[b,j,a,i]  
            Taibj[a,i,b,j]           = T2new_ab[b,j,a,i]
            T1aibj[a,i,b,j]          = T2old_ab[b,j,a,i]
#
            REQUEST                    T2old_ab[a,i,b,j]  
            REQUEST                    T2new_ab[a,i,b,j]  
            Taibj[a,i,b,j]          += T2new_ab[a,i,b,j]
            T1aibj[a,i,b,j]         += T2old_ab[a,i,b,j]

            execute energy_denominator_rhf Taibj[a,i,b,j] fock_a 
            T1aibj[a,i,b,j] -= Taibj[a,i,b,j]
            T1aibj[a,i,b,j] *= -0.5

            if niter < diis_order  
               PREPARE Eaibj[a,i,b,j,kiter] = T1aibj[a,i,b,j] 
            endif  

            if niter >= diis_order  
               kcount = 0.0 
               DO jdiis 
                  kcount += 1.0 
                  if kcount == diis_order 
                     PREPARE Eaibj[a,i,b,j,jdiis] = T1aibj[a,i,b,j] 
                     exit 
                  endif 
               ENDDO jdiis 
            endif  
#
           #if kiter == 1
           #   PREPARE e1aibj(a,i,b,j) = T1aibj(a,i,b,j)
           #endif
#
           #if kiter == 2
           #   PREPARE e2aibj(a,i,b,j) = T1aibj(a,i,b,j)
           #endif
#
           #if kiter == 3
           #   PREPARE e3aibj(a,i,b,j) = T1aibj(a,i,b,j)
           #endif
#
           #if kiter == 4
           #   PREPARE e4aibj(a,i,b,j) = T1aibj(a,i,b,j)
           #endif
#
           #if kiter >= 5
           #   PREPARE e5aibj(a,i,b,j) = T1aibj(a,i,b,j)
           #endif
#
      ENDPARDO b, a, j, i
      server_barrier 
#
      PARDO b, a, j, i
#
            REQUEST                    T2new_ab[a,i,b,j] 
            REQUEST                    T2new_ab[b,j,a,i] 
            Taibj[a,i,b,j]           = T2new_ab[b,j,a,i]
            Taibj[a,i,b,j]          += T2new_ab[a,i,b,j]
            Taibj[a,i,b,j]          *= 0.5  
            execute energy_denominator_rhf Taibj[a,i,b,j] fock_a 
            PREPARE T2old_ab[a,i,b,j]    = Taibj[a,i,b,j]
#
      ENDPARDO b, a, j, i
#
      ENDPROC UPDATET2 
#     ----------------  
#
# PPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPP 
#
# PPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPP 
#
      PROC UPDATET1 
#     ------------- 
#
      PARDO a, i 
#
            GET                t1a_new[a,i] 
            GET                t1a_old[a,i] 
            tai[a,i]         = t1a_new[a,i] 
            t1ai[a,i]        = t1a_old[a,i] 
            execute energy_denominator_rhf tai[a,i] fock_a  
           #PUT t1a_old(a,i) = tai(a,i) 
            tai[a,i]        -= t1ai[a,i] 

            if niter < diis_order  
               PUT Eai[a,i,kiter] = Tai[a,i] 
            endif  

            if niter >= diis_order  
               kcount = 0.0 
               DO jdiis 
                  kcount += 1.0 
                  if kcount == diis_order 
                     PUT Eai[a,i,jdiis] = Tai[a,i] 
                     exit 
                  endif 
               ENDDO jdiis 
            endif  
#
           #if kiter == 1 
           #   PUT e1ai(a,i) = Tai(a,i) 
           #endif 
#
           #if kiter == 2 
           #   PUT e2ai(a,i) = Tai(a,i) 
           #endif 
#
           #if kiter == 3 
           #   PUT e3ai(a,i) = Tai(a,i) 
           #endif 
#
           #if kiter == 4 
           #   PUT e4ai(a,i) = Tai(a,i) 
           #endif 
#
           #if kiter >= 5 
           #   PUT e5ai(a,i) = Tai(a,i) 
           #endif 
#
      ENDPARDO a, i 
#
      sip_barrier 
#
      PARDO a, i 
#
            GET                t1a_new[a,i] 
            tai[a,i]         = t1a_new[a,i] 
            execute energy_denominator_rhf tai[a,i] fock_a  
            PUT t1a_old[a,i] = tai[a,i] 
#
      ENDPARDO a, i 
#
      sip_barrier 
#
      ENDPROC UPDATET1 
#     ----------------  
#
      PROC MOVET2
#     ------------
#
      PARDO b, a, j, i
#
            REQUEST                    T2old_ab[a,i,b,j]  
            Taibj[a,i,b,j]           = T2old_ab[a,i,b,j]

            if niter < diis_order  
               PREPARE Daibj[a,i,b,j,kiter] = Taibj[a,i,b,j] 
            endif  

            if niter >= diis_order  
               kcount = 1.0 
               DO kdiis 
                  kcount += 1.0 
                  if kcount == diis_order 
                     PREPARE Daibj[a,i,b,j,kdiis] = Taibj[a,i,b,j] 
                     exit 
                  endif 
               ENDDO kdiis 
            endif  
#
           #if kiter == 1
           #   PREPARE d1aibj(a,i,b,j) = Taibj(a,i,b,j)
           #endif
#
           #if kiter == 2
           #   PREPARE d2aibj(a,i,b,j) = Taibj(a,i,b,j)
           #endif
#
           #if kiter == 3
           #   PREPARE d3aibj(a,i,b,j) = Taibj(a,i,b,j)
           #endif
#
           #if kiter == 4
           #   PREPARE d4aibj(a,i,b,j) = Taibj(a,i,b,j)
           #endif
#
           #if kiter >= 5
           #   PREPARE d4aibj(a,i,b,j) = Taibj(a,i,b,j)
           #endif
#
      ENDPARDO b, a, j, i
#
      ENDPROC MOVET2
#     ----------------
#
      PROC MOVET1
#     ------------
#
      PARDO a, i 
#
            GET        t1a_old[a,i]
            tai[a,i] = t1a_old[a,i]

            if niter < diis_order  
               PUT Dai[a,i,kiter] = Tai[a,i] 
            endif  

            if niter >= diis_order  
               kcount = 1.0 
               DO kdiis 
                  kcount += 1.0 
                  if kcount == diis_order 
                     PUT Dai[a,i,kdiis] = Tai[a,i] 
                     exit 
                  endif 
               ENDDO kdiis 
            endif  
#
           #if kiter == 1
           #   PUT d1ai(a,i) = tai(a,i)
           #endif
#
           #if kiter == 2
           #   PUT d2ai(a,i) = Tai(a,i)
           #endif
#
           #if kiter == 3
           #   PUT d3ai(a,i) = Tai(a,i)
           #endif
#
           #if kiter == 4
           #   PUT d4ai(a,i) = Tai(a,i)
           #endif
#
           #if kiter >= 5
           #   PUT d4ai(a,i) = Tai(a,i)
           #endif
#
      ENDPARDO a, i
#
      ENDPROC MOVET1
#     ----------------

      PROC DIISN 
#     ---------- 

      create DIST_BB 
      worder = niter 
      if niter >= diis_order 
         worder = diis_order 
      endif 
      sip_barrier 
      PARDO a, i, b, j 
         kcount = 0.0 
         DO jdiis 
            kcount += 1.0 
            if kcount <= worder  

               REQUEST Eaibj[a,i,b,j,jdiis]   
               REQUEST Eaibj[a,j,b,i,jdiis]   

               t1aibj[a,i,b,j] = Eaibj[a,i,b,j,jdiis] 
               t3aibj[a,j,b,i] = Eaibj[a,j,b,i,jdiis] 
               t2aibj[a,i,b,j] = t3aibj[a,j,b,i] 
               t1aibj[a,i,b,j]-= t2aibj[a,i,b,j] 

         jcount = 0.0 
         DO j1diis 
            jcount += 1.0 
            if jcount <= worder  

               REQUEST Eaibj[a,i,b,j,j1diis]   
               REQUEST Eaibj[a,j,b,i,j1diis]   

               t3aibj[a,i,b,j] = Eaibj[a,i,b,j,j1diis] 
               t2aibj[a,j,b,i] = Eaibj[a,j,b,i,j1diis] 
               t4aibj[a,i,b,j] = t2aibj[a,j,b,i] 
               t3aibj[a,i,b,j]-= t4aibj[a,i,b,j] 
 
               tbb[jdiis,j1diis]          = Eaibj[a,i,b,j,jdiis]*Eaibj[a,i,b,j,j1diis] 
               tbb[jdiis,j1diis]         *= 2.0  
               PUT DIST_BB[jdiis,j1diis] += tbb[jdiis,j1diis]  
 
               etemp                     = t1aibj[a,i,b,j]*t3aibj[a,i,b,j] 
               tbb[jdiis,j1diis]          = etemp 
               PUT DIST_BB[jdiis,j1diis] += tbb[jdiis,j1diis]  
            endif 
         ENDDO j1diis 
            endif # kcount <= diis_order  
         ENDDO jdiis 
      ENDPARDO a, i, b, j 

      PARDO a, i 
         kcount = 0.0 
         DO jdiis 
            kcount += 1.0 
            if kcount <= worder  

               GET Eai[a,i,jdiis] 

         jcount = 0.0 
         DO j1diis 
            jcount += 1.0 
            if jcount <= worder  

               GET Eai[a,i,j1diis] 

               tbb[jdiis,j1diis]          = Eai[a,i,jdiis]*Eai[a,i,j1diis] 
               tbb[jdiis,j1diis]         *= 4.0  
               PUT DIST_BB[jdiis,j1diis] += tbb[jdiis,j1diis]  
 
            endif 
         ENDDO j1diis 
            endif # kcount <= diis_order  
         ENDDO jdiis 
      ENDPARDO a, i  

      sip_barrier 

      jcount = 0.0 
      DO jdiis 
         jcount += 1.0 
         if jcount <= worder  
      kcount = 0.0 
      DO j1diis 
         kcount += 1.0 
         if kcount <= worder  
            GET DIST_BB[jdiis,j1diis] 
            BB[jdiis,j1diis] = DIST_BB[jdiis,j1diis] 
         endif 
      ENDDO j1diis 
         endif 
      ENDDO jdiis 
     #println "finished coputing BB" 

      execute compute_diis BB 
      temp_order = worder 
     #if worder > diis_order 
     #   temp_order = diis_order 
     #endif 
      sip_barrier 
     #println "finished compute_diis" 
     #DO jdiis 
     #execute print_block BB(jdiis,jdiis) 
     #ENDDO jdiis 

      PARDO a, i, b, j 
            taibj[a,i,b,j] = 0.0 
            n1 = 0.0  
            DO kdiis   
               n1 += 1.0  
               if n1 <  worder # temp_order # VFL  
                  REQUEST           Daibj[a,i,b,j,kdiis]   
                  t2aibj[a,i,b,j] = Daibj[a,i,b,j,kdiis] 
                  n2 = 0.0  
                  DO d2   
                    n2 += 1.0  
                    if n2 <= worder 
                      kcount  = n2 
                      kcount -= n1 
                      if kcount == one 
                         REQUEST           Eaibj[a,i,b,j,d2]   
                         t3aibj[a,i,b,j] = Eaibj[a,i,b,j,d2] 
                         execute return_sval BB[d2,d2] etemp 
                        #etemp        = BB(d1,d1)        
                         t3aibj[a,i,b,j] += t2aibj[a,i,b,j] 
                         t3aibj[a,i,b,j] *= etemp   
                         taibj[a,i,b,j]  += t3aibj[a,i,b,j]  
                      endif 
                    endif #n2 <= worder 
                   ENDDO d2 
               endif 
            ENDDO kdiis  

            n2 = 0.0  
            DO d2   
               n2 += 1.0  
               if n2 == one 
                  REQUEST           D0aibj[a,i,b,j]  
                  t2aibj[a,i,b,j] = D0aibj[a,i,b,j] 
                  REQUEST           Eaibj[a,i,b,j,d2]   
                  t3aibj[a,i,b,j] = Eaibj[a,i,b,j,d2] 
                  execute return_sval BB[d2,d2] etemp 
                 #etemp        = BB(d1,d1)        
                  t3aibj[a,i,b,j] += t2aibj[a,i,b,j] 
                  t3aibj[a,i,b,j] *= etemp   
                  taibj[a,i,b,j]  += t3aibj[a,i,b,j]  
               endif 
            ENDDO d2 

            PREPARE T2old_ab[a,i,b,j] = taibj[a,i,b,j]
      ENDPARDO a, i, b, j 

      PARDO a, i 
            tai[a,i] = 0.0 
            n1 = 0.0  
            DO kdiis    
               n1 += 1.0  
            if n1 <  worder # temp_order # VFL  
               GET         Dai[a,i,kdiis] 
               t2ai[a,i] = Dai[a,i,kdiis] 
            n2 = 0.0  
            DO d2   
               n2 += 1.0  
            if n2 <= worder 
               kcount  = n2 
               kcount -= n1 
               if kcount == one 
                  GET         Eai[a,i,d2] 
                  t3ai[a,i] = Eai[a,i,d2] 
                  execute return_sval BB[d2,d2] etemp 
                 #etemp        = BB(d1,d1)        
                  t3ai[a,i] += t2ai[a,i] 
                  t3ai[a,i] *= etemp   
                  tai[a,i]  += t3ai[a,i]  
               endif 
            endif 
            ENDDO d2   
            endif 
            ENDDO kdiis 

            n2 = 0.0  
            DO d2   
               n2 += 1.0  
               if n2 == one 
                  REQUEST D0ai[a,i]  
                  t2ai[a,i] = D0ai[a,i] 
                  GET         Eai[a,i,d2] 
                  t3ai[a,i] = Eai[a,i,d2] 
                  execute return_sval BB[d2,d2] etemp 
                 #etemp        = BB(d1,d1)        
                  t3ai[a,i] += t2ai[a,i] 
                  t3ai[a,i] *= etemp   
                  tai[a,i]  += t3ai[a,i]  
               endif 
            ENDDO d2   

            PUT t1a_old[a,i] = tai[a,i]  
      ENDPARDO a, i  

      server_barrier

# If history space is full shift history end error vectors 

      if niter >= diis_order  

# histories first 

      PARDO a, i, b, j 
      n1 = 0.0 
      DO kdiis 
         n1 += 1.0  
         if n1 <  worder # temp_order # VFL 
         REQUEST Daibj[a,i,b,j,kdiis]   
         taibj[a,i,b,j] = Daibj[a,i,b,j,kdiis] 
         n2 = 0.0 
      DO k1diis 
         n2 += 1.0  
         etemp = n1 
         etemp -= n2 
         if etemp == 1.0 
            PREPARE Daibj[a,i,b,j,k1diis] = taibj[a,i,b,j] 
         endif 
         if n1 == one 
            PREPARE D0aibj[a,i,b,j] = taibj[a,i,b,j] 
         endif 
      ENDDO k1diis  
         endif 
      ENDDO kdiis 
      ENDPARDO a, i, b, j 

      PARDO a, i  
      n1 = 0.0 
      DO kdiis 
         n1 += 1.0  
         if n1 <  worder # temp_order # VFL 
         GET Dai[a,i,kdiis]   
         tai[a,i] = Dai[a,i,kdiis]   
         n2 = 0.0 
      DO k1diis 
         n2 += 1.0  
         etemp = n1 
         etemp -= n2 
         if etemp == 1.0 
            PUT Dai[a,i,k1diis] = tai[a,i] 
         endif 
         if n1 == one 
            PREPARE D0ai[a,i] = tai[a,i] 
         endif 
      ENDDO k1diis  
         endif 
      ENDDO kdiis 
      ENDPARDO a, i  

# Now error vectors 

      PARDO a, i, b, j 
      n1 = 0.0 
      DO kdiis 
         n1 += 1.0  
         if n1 <= worder # temp_order # VFL 
         REQUEST Eaibj[a,i,b,j,kdiis]   
         taibj[a,i,b,j] = Eaibj[a,i,b,j,kdiis] 
         n2 = 0.0 
      DO k1diis 
         n2 += 1.0  
         etemp = n1 
         etemp -= n2 
         if etemp == 1.0 
            PREPARE Eaibj[a,i,b,j,k1diis] = taibj[a,i,b,j] 
         endif 
      ENDDO k1diis  
         endif 
      ENDDO kdiis 
      ENDPARDO a, i, b, j 

      PARDO a, i  
      n1 = 0.0 
      DO kdiis 
         n1 += 1.0  
         if n1 <= worder # temp_order # VFL 
         GET Eai[a,i,kdiis]   
         tai[a,i] = Eai[a,i,kdiis]   
         n2 = 0.0 
      DO k1diis 
         n2 += 1.0  
         etemp = n1 
         etemp -= n2 
         if etemp == 1.0 
            PUT Eai[a,i,k1diis] = tai[a,i] 
         endif 
      ENDDO k1diis  
         endif 
      ENDDO kdiis 
      ENDPARDO a, i  


      endif 
      server_barrier
      delete DIST_BB 

      ENDPROC DIISN 
#     ------------- 
#
# ---------------------------------------------------------------------------------
# 
# BEGIN MAIN PROGRAM 
# ------------------ 
#
      ecrit = cc_conv
      zero = 0.0 
      one = 1.0
      two = 2.0
      five  = 5.0
      six   = 6.0
      seven = 7.0

#     diis_count = 0.0  
#     diis_start = 2.0  
#     diis_end = 5000.0 
#     diis_order = 8.0  
#     diis_max_order = 10.0 

      diis_start = 0.0
      do kbeg 
         diis_start += 1.0
      ENDDO kbeg 
      diis_start = 2.0

      diis_order = 0.0
      do korder
         diis_order += 1.0
      ENDDO korder

      print diis_start
      print diis_order

     #create Dca 
     #create Fta 
      sip_barrier 
#
# Read transformed integrals from lists 
# ------------------------------------- 
#
      CALL READ_2EL 

      CREATE t1a_old 
      CREATE t1a_new 
      CREATE Dai 
      CREATE Eai 
#
      SIP_BARRIER  
#
# Get initial second-order guess 
# ------------------------------ 
      CALL IGUESS_UHF
      SERVER_BARRIER  
# 
# zero out the diis vectors 
     #PARDO jdiis, j1diis 
     #         tbb(jdiis,j1diis)         = 0.0  
     #         PUT DIST_BB(jdiis,j1diis) = tbb(jdiis,j1diis)  
     #ENDPARDO jdiis, j1diis 

      PARDO a, i, b, j 
            REQUEST                     T2old_ab[a,i,b,j]  
            REQUEST                     T2old_ab[a,j,b,i]  
            taiai[a,i,b,j]            = T2old_ab[a,i,b,j] 
            t1aiai[a,i,b,j]           = T2old_ab[a,j,b,i] 
            taiai[a,i,b,j]           -= t1aiai[a,i,b,j] 
            PREPARE T2old_aa[a,i,b,j] = taiai[a,i,b,j] 
      ENDPARDO a, i, b, j 
#
      SERVER_BARRIER  
#
# Calculate the second-order energy 
# --------------------------------- 
      CALL ENERGY_NEW 

# Start iterations
# ---------------- 
      niter = 0.0 
      DO KITER 
         niter += 1.0 

         SERVER_BARRIER  
#
         PARDO a, i, b, j 
               REQUEST                     T2old_ab[a,i,b,j]  
               REQUEST                     T2old_ab[a,j,b,i]  
               taiai[a,i,b,j]            = T2old_ab[a,i,b,j] 
               t1aiai[a,i,b,j]           = T2old_ab[a,j,b,i] 
               taiai[a,i,b,j]           -= t1aiai[a,i,b,j] 
               PREPARE T2old_aa[a,i,b,j] = taiai[a,i,b,j] 
         ENDPARDO a, i, b, j 
#
         SERVER_BARRIER  
#
#        Form Tau and Taup 
#        ----------------- 
         CALL TAU  
#
#        Zero-out the new two-particle amplitude arrays
#        ---------------------------------------------- 
#
         CALL T2NEW_ZERO
#
#        Form one-particle intermediates 
#        ------------------------------- 
         CREATE Fae_a 
         CREATE Fme_a
         CREATE Fmi_a 
         SERVER_BARRIER  
# 
         CALL FAE 
         CALL FMIA 
         CALL FMEA
         SERVER_BARRIER  
#
#        Calculate new one-particle amplitudes 
#        ------------------------------------- 
#
         CALL T1ANEW 
#
#        Calculate new two-particle amplitudes 
#        ------------------------------------- 
#
         CALL T2NEWAB  
#
         CALL WMINJ 
         CALL AOLADDER 
        #CALL AOLADDER_GPU 
         CALL WAEBF  
         CALL WMEBJ 
#
#        Wait for all to finish 
#        ---------------------- 
         SERVER_BARRIER   
#
         DELETE Fae_a 
         DELETE Fme_a
         DELETE Fmi_a 
         CALL UPDATET1 
         CALL UPDATET2 
#
         SERVER_BARRIER   
         CALL ENERGY_NEW 
         SERVER_BARRIER   
         if niter >= diis_start # two  
            CALL DIISN 
         ENDIF 
#
         CALL MOVET1
         CALL MOVET2
#
         SERVER_BARRIER  
         PARDO a, i, b, j 
               REQUEST                     T2old_ab[a,i,b,j]  
               REQUEST                     T2old_ab[a,j,b,i]  
               taiai[a,i,b,j]            = T2old_ab[a,i,b,j] 
               t1aiai[a,i,b,j]           = T2old_ab[a,j,b,i] 
               taiai[a,i,b,j]           -= t1aiai[a,i,b,j] 
               PREPARE T2old_aa[a,i,b,j] = taiai[a,i,b,j] 
         ENDPARDO a, i, b, j 
         SERVER_BARRIER  
#
#        Check on convergence 
#        -------------------- 
#
         IF ecorrT < eold 
            ediff = eold - ecorrT 
            IF ediff < ecrit 
               exit # kiter 
            ENDIF
         ENDIF   
#
         IF ecorrT > eold 
            ediff = ecorrT - eold  
            IF ediff < ecrit 
               exit # kiter 
            ENDIF
         ENDIF   
#
#        Reset eold --> enew 
#        ------------------- 
#
         eold = ecorrT 

      ENDDO KITER 
#
      server_barrier 
      restore_persistent scf_energy "scf_energy"
      server_barrier 
      ccsd_correlation = ecorrT
      totenerg  = ecorrT  
      totenerg += scf_energy
      ccsd_energy = totenerg
      print ccsd_correlation
      print ccsd_energy
#
######CALL WRITE_2EL   
#
      set_persistent t1a_old "t1a_old"
      set_persistent T2old_aa "T2old_aa"
      set_persistent T2old_ab "T2old_ab"
      set_persistent scf_energy "scf_energy"
      set_persistent totenerg "totenerg"
      set_persistent VSpipi "VSpipi"
      set_persistent Vaaii  "Vaaii"
      set_persistent Viaai  "Viaai"
      set_persistent Vaaai  "Vaaai"
      set_persistent Vpiqj  "Vpiqj"
#
                           ENDSIAL CCSD_RHF_SV1    
#
# EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 
#
#
